
(*/chapter Logical lemmata *)

(*/section Propositional logic *)

01. 1 ⊢ A ↔ B, hypo.
02. 1 ⊢ A → B, bij_eliml 1.
lsubj_elim. ⊢ (A ↔ B) → A → B, subj_intro 2.

01. 1 ⊢ A ↔ B, hypo.
02. 1 ⊢ B → A, bij_elimr 1.
rsubj_elim. ⊢ (A ↔ B) → B → A, subj_intro 2.

01. 1 ⊢ A, hypo.
02. 2 ⊢ ¬A, hypo.
03. 1, 2 ⊢ ⊥, neg_elim 2 1.
04. 1 ⊢ ¬¬A, neg_intro 3.
dn_intro. ⊢ A → ¬¬A, subj_intro 4.

01. 1 ⊢ ¬B, hypo.
02. 2 ⊢ A → B, hypo.
03. 3 ⊢ A, hypo.
04. 2, 3 ⊢ B, subj_elim 2 3.
05. 1, 2, 3 ⊢ ⊥, neg_elim 1 4.
06. 2, 1 ⊢ ¬A, neg_intro 5.
07. 2 ⊢ ¬B → ¬A, subj_intro 6.
contraposition. ⊢ (A → B) → ¬B → ¬A, subj_intro 7.

01. 1 ⊢ A ↔ B, hypo.
02. 2 ⊢ ¬A, hypo.
03. 3 ⊢ B, hypo.
04. 1, 3 ⊢ A, rsubj_elim 1 3.
05. 1, 2, 3 ⊢ ⊥, neg_elim 2 4.
06. 1, 2 ⊢ ¬B, neg_intro 5.
07. 1 ⊢ ¬A → ¬B, subj_intro 6.
08. 8 ⊢ ¬B, hypo.
09. 9 ⊢ A, hypo.
10. 1, 9 ⊢ B, lsubj_elim 1 9.
11. 1, 8, 9 ⊢ ⊥, neg_elim 8 10.
12. 1, 8 ⊢ ¬A, neg_intro 11.
13. 1 ⊢ ¬B → ¬A, subj_intro 12.
14. 1 ⊢ ¬A ↔ ¬B, bij_intro 7 13.
equi_cong_neg. ⊢ (A ↔ B) → (¬A ↔ ¬B),
  subj_intro 14.

01. 1 ⊢ ¬¬A, hypo.
02. ⊢ A ∨ ¬A, lem.
03. 3 ⊢ A, hypo.
04. 4 ⊢ ¬A, hypo.
05. 1, 4 ⊢ ⊥, neg_elim 1 4.
06. 1, 4 ⊢ A, efq 5.
07. 1 ⊢ A, disj_elim 2 3 6.
dne. ⊢ ¬¬A → A, subj_intro 7.

01. 1 ⊢ A → ¬B, hypo.
02. 2 ⊢ A ∧ B, hypo.
03. 2 ⊢ A, conj_eliml 2.
04. 1, 2 ⊢ ¬B, subj_elim 1 3.
05. 2 ⊢ B, conj_elimr 2.
06. 1, 2 ⊢ ⊥, neg_elim 4 5.
07. 1 ⊢ ¬(A ∧ B), neg_intro 6.
lemma_i. ⊢ (A → ¬B) → ¬(A ∧ B), subj_intro 7.

01. 1 ⊢ A ∧ ¬(A ∧ B), hypo.
02. 1 ⊢ A, conj_eliml 1.
03. 1 ⊢ ¬(A ∧ B), conj_elimr 1.
04. 4 ⊢ B, hypo.
05. 1, 4 ⊢ A ∧ B, conj_intro 2 4.
06. 1, 4 ⊢ ⊥, neg_elim 3 5.
07. 1 ⊢ ¬B, neg_intro 6.
lemma_ii. ⊢ A ∧ ¬(A ∧ B) → ¬B, subj_intro 7.

01. 1 ⊢ ¬(A ∧ ¬B), hypo.
02. 2 ⊢ A, hypo.
03. 3 ⊢ ¬B, hypo.
04. 2, 3 ⊢ A ∧ ¬B, conj_intro 2 3.
05. 1, 2, 3 ⊢ ⊥, neg_elim 1 4.
06. 1, 2 ⊢ ¬¬B, neg_intro 5.
07. 1, 2 ⊢ B, dne 6.
08. 1 ⊢ A → B, subj_intro 7.
lemma_iii. ⊢ ¬(A ∧ ¬B) → A → B, subj_intro 8.


(*/section First order logic *)

(*/subsection Pure first order logic *)

01. 1 ⊢ ¬∃x. Q x, hypo.
02. 2 ⊢ Q x, hypo.
03. 2 ⊢ ∃x. Q x, ex_intro 2.
04. 1, 2 ⊢ ⊥, neg_elim 1 3.
05. 1 ⊢ ¬Q x, neg_intro 4.
06. 1 ⊢ ∀x. ¬Q x, uq_intro 5.
neg_ex. ⊢ (¬∃x. Q x) → (∀x. ¬Q x), subj_intro 6.

(*/subsection First order logic with equality *)

01. 1 ⊢ x = y, hypo.
02. 1 ⊢ y = x, eq_subst 1 eq_refl, Q t ↔ t = x.
eq_symm. ⊢ x = y → y = x, subj_intro 2.

01. 1 ⊢ y = x, hypo.
02. 1 ⊢ x = y, eq_symm 1.
03. 1 ⊢ Q x → Q y, subj_elim eq_subst 2.
eq_subst_rev. ⊢ y = x → Q x → Q y, subj_intro 3.

(*/section Modal logic *)

(*/subsection General lemmata *)

pos_unfold. ⊢ ◇A → ¬□¬A, bij_eliml pos_equi.
pos_fold. ⊢ ¬□¬A → ◇A, bij_elimr pos_equi.

01. 1 ⊢ ¬◇A, hypo.
02. 2 ⊢ ¬□¬A, hypo.
03. 2 ⊢ ◇A, pos_fold 2.
04. 1, 2 ⊢ ⊥, neg_elim 1 3.
05. 1 ⊢ ¬¬□¬A, neg_intro 4.
06. 1 ⊢ □¬A, dne 5.
pos_corollary. ⊢ ¬◇A → □¬A, subj_intro 6.


(*/subsection Lemmata of modal logic K *)

01. 1 ⊢ □(A → B → C), hypo.
02. 1 ⊢ □A → □(B → C), K 1.
03. 3 ⊢ □A, hypo.
04. 1, 3 ⊢ □(B → C), subj_elim 2 3.
05. 1, 3 ⊢ □B → □C, K 4.
06. 1 ⊢ □A → □B → □C, subj_intro 5.
Kii. ⊢ □(A → B → C) → □A → □B → □C, subj_intro 6.

01. 1 ⊢ A ∧ B, hypo.
02. 1 ⊢ A, conj_eliml 1.
03. 1 ⊢ B, conj_elimr 1.
04. 1 ⊢ B ∧ A, conj_intro 3 2.
05. ⊢ A ∧ B → B ∧ A, subj_intro 4.
06. ⊢ ¬(B ∧ A) → ¬(A ∧ B), contraposition 5.
07. ⊢ □(¬(B ∧ A) → ¬(A ∧ B)), nec 6.
08. ⊢ □¬(B ∧ A) → □¬(A ∧ B), K 7.
09. ⊢ ¬□¬(A ∧ B)→ ¬□¬(B ∧ A), contraposition 8.
10. 10 ⊢ ◇(A ∧ B), hypo.
11. 10 ⊢ ¬□¬(A ∧ B), pos_unfold 10.
12. 10 ⊢ ¬□¬(B ∧ A), subj_elim 9 11.
13. 10 ⊢ ◇(B ∧ A), pos_fold 12.
K_pos_conj_comm. ⊢ ◇(A ∧ B) → ◇(B ∧ A),
  subj_intro 13.

01. 1 ⊢ □A, hypo.
02. 2 ⊢ ◇¬A, hypo.
03. ⊢ ◇¬A ↔ ¬□¬¬A, pos_equi.
04. 2 ⊢ ¬□¬¬A, lsubj_elim 3 2.
05. ⊢ A → ¬¬A, dn_intro.
06. ⊢ □(A → ¬¬A), nec 5.
07. ⊢ □A → □¬¬A, K 6.
08. 1 ⊢ □¬¬A, subj_elim 7 1.
09. 1, 2 ⊢ ⊥, neg_elim 4 8.
10. 1 ⊢ ¬◇¬A, neg_intro 9.
K_lemma_i. ⊢ □A → ¬◇¬A, subj_intro 10.

01. 1 ⊢ □(A → B), hypo.
02. ⊢ □((A → B) → (¬B → ¬A)), nec contraposition.
03. ⊢ □(A → B) → □(¬B → ¬A), K 2.
04. 1 ⊢ □(¬B → ¬A), subj_elim 3 1.
05. 1 ⊢ □¬B → □¬A, K 4.
06. 1 ⊢ ¬□¬A → ¬□¬B, contraposition 5.
07. ⊢ ◇A ↔ ¬□¬A, pos_equi.
08. ⊢ ◇B ↔ ¬□¬B, pos_equi.
09. 9 ⊢ ◇A, hypo.
10. 9 ⊢ ¬□¬A, lsubj_elim 7 9.
11. 1, 9 ⊢ ¬□¬B, subj_elim 6 10.
12. 1, 9 ⊢ ◇B, rsubj_elim 8 11.
13. 1 ⊢ ◇A → ◇B, subj_intro 12.
K_lemma_ii. ⊢ □(A → B) → ◇A → ◇B,
  subj_intro 13.

01. ⊢ A → ¬¬A, dn_intro.
02. ⊢ □(A → ¬¬A), nec 1.
03. ⊢ ◇A → ◇¬¬A, K_lemma_ii 2.
K_lemma_iii. ⊢ ¬◇¬¬A → ¬◇A, contraposition 3.

01. 1 ⊢ A → □A, hypo.
02. 2 ⊢ A, hypo.
03. 1, 2 ⊢ □A, subj_elim 1 2.
04. 1, 2 ⊢ ¬◇¬A, K_lemma_i 3.
05. 1 ⊢ A → ¬◇¬A, subj_intro 4.
06. 1 ⊢ ¬(A ∧ ◇¬A), lemma_i 5.
07. ⊢ (A → □A) → ¬(A ∧ ◇¬A), subj_intro 6.
08. ⊢ □((A → □A) → ¬(A ∧ ◇¬A)), nec 7.
09. ⊢ □(A → □A) → □¬(A ∧ ◇¬A), K 8.
10. 10 ⊢ □(A → □A), hypo.
11. 10 ⊢ □¬(A ∧ ◇¬A), subj_elim 9 10.
12. 10 ⊢ ¬◇¬¬(A ∧ ◇¬A), K_lemma_i 11.
13. 10 ⊢ ¬◇(A ∧ ◇¬A), K_lemma_iii 12.
K_lemma_iv. ⊢ □(A → □A) → ¬◇(A ∧ ◇¬A),
  subj_intro 13.

01. 1 ⊢ A, hypo.
02. 2 ⊢ B, hypo.
03. 1, 2 ⊢ A ∧ B, conj_intro 1 2.
04. 1 ⊢ B → A ∧ B, subj_intro 3.
05. ⊢ A → B → A ∧ B, subj_intro 4.
06. ⊢ □(A → B → A ∧ B), nec 5.
K_lemma_v. ⊢ □A → □B → □(A ∧ B), Kii 6.

01. 1 ⊢ □A ∧ ◇B, hypo.
02. 1 ⊢ □A, conj_eliml 1.
03. 1 ⊢ ◇B, conj_elimr 1.
04. 1 ⊢ ¬□¬B, pos_unfold 3.
05. 5 ⊢ ¬◇(A ∧ B), hypo.
06. 5 ⊢ □¬(A ∧ B), pos_corollary 5.
07. 1, 5 ⊢ □(A ∧ ¬(A ∧ B)), K_lemma_v 2 6.
08. ⊢ □(A ∧ ¬(A ∧ B) → ¬B), nec lemma_ii.
09. ⊢ □(A ∧ ¬(A ∧ B)) → □¬B, K 8.
10. 1, 5 ⊢ □¬B, subj_elim 9 7.
11. 1, 5 ⊢ ⊥, neg_elim 4 10.
12. 1 ⊢ ¬¬◇(A ∧ B), neg_intro 11.
13. 1 ⊢ ◇(A ∧ B), dne 12.
K_lemma_vi. ⊢ □A ∧ ◇B → ◇(A ∧ B), subj_intro 13.

(*/subsection Lemmata of modal logic KB *)

01. 1 ⊢ A → □A, hypo.
02. 1 ⊢ ¬□A → ¬A, contraposition 1.
03. ⊢ ¬A → □◇¬A, B.
04. 4 ⊢ ¬□A, hypo.
05. 1, 4 ⊢ ¬A, subj_elim 2 4.
06. 1, 4 ⊢ □◇¬A, subj_elim 3 5.
07. 1 ⊢ ¬□A → □◇¬A, subj_intro 6.
KB_lemma_i. ⊢ (A → □A) → (¬□A → □◇¬A), subj_intro 7.

01. 1 ⊢ A → □A, hypo.
02. 2 ⊢ □(A → □A), hypo.
03. 3 ⊢ ◇A ∧ ¬□A, hypo.
04. 3 ⊢ ◇A, conj_eliml 3.
05. 3 ⊢ ¬□A, conj_elimr 3.
06. 1, 3 ⊢ □◇¬A, KB_lemma_i 1 5.
07. 1, 3 ⊢ □◇¬A ∧ ◇A, conj_intro 6 4.
08. 1, 3 ⊢ ◇(◇¬A ∧ A), K_lemma_vi 7.
09. 1, 3 ⊢ ◇(A ∧ ◇¬A), K_pos_conj_comm 8.
10. 2 ⊢ ¬◇(A ∧ ◇¬A), K_lemma_iv 2.
11. 1, 2, 3 ⊢ ⊥, neg_elim 10 9.
12. 1, 2 ⊢ ¬(◇A ∧ ¬□A), neg_intro 11.
13. 1, 2 ⊢ ◇A → □A, lemma_iii 12.
14. 1 ⊢ □(A → □A) → ◇A → □A, subj_intro 13.
KB_lemma_ii. ⊢ (A → □A) → □(A → □A) → ◇A → □A,
  subj_intro 14.
