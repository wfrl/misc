
(*/chapter Grothendieck universes *)

univ_equi. ⊢ univ U ↔ ∅ ∈ U ∧
  (∀x. x ∈ U → x ⊆ U) ∧
  (∀x. x ∈ U → power x ∈ U) ∧
  (∀I. ∀x. I ∈ U → map x I U → ⋃(img x I) ∈ U), def.

least_univ_eq. ⊢ least_univ A = ⋂{U | univ U ∧ A ∈ U}, def.
Univ_eq. ⊢ Univ = least_univ onat, def.
universes. ⊢ set x → ∃U. set U ∧ univ U ∧ x ∈ U, axiom.

01. 1 ⊢ univ U, hypo.
02. 1 ⊢ ∅ ∈ U, lsubj_conj_elimlll univ_equi 1.
univ_contains_empty_set. ⊢ univ U → ∅ ∈ U, subj_intro 2.

01. 1 ⊢ univ U, hypo.
02. 1 ⊢ (∀x. x ∈ U → x ⊆ U), lsubj_conj_elimllr univ_equi 1.
03. 1 ⊢ x ∈ U → x ⊆ U, uq_elim 2.
univ_trans. ⊢ univ U → x ∈ U → x ⊆ U, subj_intro 3.

01. 1 ⊢ univ U, hypo.
02. 1 ⊢ (∀x. x ∈ U → power x ∈ U), lsubj_conj_elimlr univ_equi 1.
03. 1 ⊢ x ∈ U → power x ∈ U, uq_elim 2.
univ_closed_power. ⊢ univ U → x ∈ U → power x ∈ U, subj_intro 3.

01. 1 ⊢ univ U, hypo.
02. 1 ⊢ ∀I. ∀x. I ∈ U → map x I U → ⋃(img x I) ∈ U,
  lsubj_conj_elimr univ_equi 1.
03. 1 ⊢ ∀x. I ∈ U → map x I U → ⋃(img x I) ∈ U, uq_elim 2.
04. 1 ⊢ I ∈ U → map x I U → ⋃(img x I) ∈ U, uq_elim 3.
univ_closed_family_union. ⊢ univ U → I ∈ U → map x I U →
  ⋃(img x I) ∈ U, subj_intro 4.

01. 1 ⊢ univ U, hypo.
02. 2 ⊢ y ⊆ x, hypo.
03. 3 ⊢ x ∈ U, hypo.
04. 1, 3 ⊢ power x ∈ U, univ_closed_power 1 3.
05. 1, 3 ⊢ power x ⊆ U, univ_trans 1 4.
06. 3 ⊢ set x, set_intro 3.
07. 2, 3 ⊢ set y, subset 2 6.
08. 2, 3 ⊢ y ∈ power x, power_intro 7 2.
09. 1, 2, 3 ⊢ y ∈ U, incl_elim 5 8.
univ_closed_subset. ⊢ univ U → y ⊆ x → x ∈ U → y ∈ U,
  subj_intro_iii 9.

01. 1 ⊢ univ U, hypo.
02. 2 ⊢ x ∈ U, hypo.
03. ⊢ x ∩ y ⊆ x, intersection_incl_left.
04. 1, 2 ⊢ x ∩ y ∈ U, univ_closed_subset 1 3 2.
univ_closed_intersection. ⊢ univ U → x ∈ U → x ∩ y ∈ U,
  subj_intro_ii 4.

01. 1 ⊢ univ U, hypo.
02. 1 ⊢ ∅ ∈ U, univ_contains_empty_set 1.
03. 1 ⊢ power ∅ ∈ U, univ_closed_power 1 2.
04. 1 ⊢ {∅} ∈ U, eq_subst power_empty_set 3, P u ↔ u ∈ U.
05. 1 ⊢ power {∅} ∈ U, univ_closed_power 1 4.
06. ⊢ power {∅} = {∅, {∅}}, power_sg empty_set_is_set.
07. 1 ⊢ {∅, {∅}} ∈ U, eq_subst 6 5, P u ↔ u ∈ U.
08. 8 ⊢ x ∈ U, hypo.
09. 9 ⊢ y ∈ U, hypo.
10. ⊢ set ∅, empty_set_is_set.
11. ⊢ set {∅}, set_sg 10.
12. 8 ⊢ map {(∅, x)} {∅} U, sg_map 10 8.
13. 9 ⊢ map {({∅}, y)} {{∅}} U, sg_map 11 9.
14. ⊢ {∅} ∩ {{∅}} = ∅,
  disjoint_sg_from_neq 10 11 zero_neq_one.
15. 8, 9 ⊢ map {(∅, x), ({∅}, y)} {∅, {∅}} U,
  map_union_eq_rngs 12 13 14.
16. 1, 8, 9 ⊢ ⋃(img {(∅, x), ({∅}, y)} {∅, {∅}}) ∈ U,
  univ_closed_family_union 1 7 15.
17. 8 ⊢ set x, set_intro 8.
18. 9 ⊢ set y, set_intro 9.
19. 8, 9 ⊢ img {(∅, x), ({∅}, y)} {∅, {∅}} = {x, y}, img_graph2 17 18.
20. 1, 8, 9 ⊢ ⋃{x, y} ∈ U, eq_subst 19 16, P t ↔ ⋃t ∈ U.
21. 8, 9 ⊢ set x ∧ set y, conj_intro 17 18.
22. 8, 9 ⊢ ⋃{x, y} = x ∪ y, Union_pair_set 21.
23. 1, 8, 9 ⊢ x ∪ y ∈ U, eq_subst 22 20, P t ↔ t ∈ U.
univ_closed_union. ⊢ univ U → x ∈ U → y ∈ U → x ∪ y ∈ U,
  subj_intro_iii 23.

01. 1 ⊢ univ U, hypo.
02. 2 ⊢ x ∈ U, hypo.
03. 2 ⊢ set x, set_intro 2.
04. 2 ⊢ {x} ⊆ power x, sg_incl_in_power 3.
05. 2 ⊢ set {x}, set_sg 3.
06. 2 ⊢ {x} ∈ power (power x), power_intro 5 4.
07. 1, 2 ⊢ power x ∈ U, univ_closed_power 1 2.
08. 1, 2 ⊢ power (power x) ∈ U, univ_closed_power 1 7.
09. 1, 2 ⊢ power (power x) ⊆ U, univ_trans 1 8.
10. 1, 2 ⊢ {x} ∈ U, incl_elim 9 6.
univ_closed_sg. ⊢ univ U → x ∈ U → {x} ∈ U, subj_intro_ii 10.

01. 1 ⊢ univ U, hypo.
02. 2 ⊢ x ∈ U, hypo.
03. 3 ⊢ y ∈ U, hypo.
04. 1, 2 ⊢ {x} ∈ U, univ_closed_sg 1 2.
05. 1, 3 ⊢ {y} ∈ U, univ_closed_sg 1 3.
06. 1, 2, 3 ⊢ {x, y} ∈ U, univ_closed_union 1 4 5.
univ_closed_pair_set. ⊢ univ U → x ∈ U → y ∈ U → {x, y} ∈ U,
  subj_intro_iii 6.

01. 1 ⊢ univ U, hypo.
02. 2 ⊢ x ∈ U, hypo.
03. 3 ⊢ y ∈ U, hypo.
04. 1, 2 ⊢ {x} ∈ U, univ_closed_sg 1 2.
05. 1, 2, 3 ⊢ {x, y} ∈ U, univ_closed_pair_set 1 2 3.
06. 1, 2, 3 ⊢ {{x}, {x, y}} ∈ U, univ_closed_pair_set 1 4 5.
07. 1, 2, 3 ⊢ (x, y) ∈ U, eq_subst_rev pair_eq 6, P u ↔ u ∈ U.
univ_closed_pair. ⊢ univ U → x ∈ U → y ∈ U → (x, y) ∈ U,
  subj_intro_iii 7.

01. 1 ⊢ univ U, hypo.
02. 2 ⊢ A ∈ U, hypo.
03. ⊢ {x | x ∈ A ∧ P x} ⊆ A, sep_is_subclass.
04. 1, 2 ⊢ {x | x ∈ A ∧ P x} ∈ U, univ_closed_subset 1 3 2.
univ_closed_sep. ⊢ univ U → A ∈ U → {x | x ∈ A ∧ P x} ∈ U,
  subj_intro_ii 4.

01. 1 ⊢ univ U, hypo.
02. 2 ⊢ A ∈ U, hypo.
03. 3 ⊢ B ∈ U, hypo.
04. 1, 2, 3 ⊢ A ∪ B ∈ U, univ_closed_union 1 2 3.
05. 1, 2, 3 ⊢ power (A ∪ B) ∈ U, univ_closed_power 1 4.
06. 1, 2, 3 ⊢ power (power (A ∪ B)) ∈ U, univ_closed_power 1 5.
07. ⊢ A × B ⊆ power (power (A ∪ B)), prod_incl_in_power.
08. 1, 2, 3 ⊢ A × B ∈ U, univ_closed_subset 1 7 6.
univ_closed_prod. ⊢ univ U → A ∈ U → B ∈ U → A × B ∈ U,
  subj_intro_iii 8.

01. 1 ⊢ univ U, hypo.
02. 2 ⊢ M ∈ U, hypo.
03. 3 ⊢ ¬M = ∅, hypo.
04. 3 ⊢ ∃y. y ∈ M, non_empty 3.
05. 5 ⊢ y ∈ M, hypo.
06. ⊢ ⋂M = ⋂M, eq_refl.
07. 5 ⊢ ⋂M ⊆ y, Intersection_is_lower_bound 6 5.
08. 1, 2 ⊢ M ⊆ U, univ_trans 1 2.
09. 1, 2, 5 ⊢ y ∈ U, incl_elim 8 5.
10. 1, 2, 5 ⊢ ⋂M ∈ U, univ_closed_subset 1 7 9.
11. 1, 2, 3 ⊢ ⋂M ∈ U, ex_elim 4 10.
univ_closed_Intersection. ⊢ univ U → M ∈ U → ¬M = ∅ → ⋂M ∈ U,
  subj_intro_iii 11.

01. 1 ⊢ univ U, hypo.
02. 2 ⊢ M ∈ U, hypo.
03. ⊢ map (id M) M M, id_is_map.
04. 1, 2 ⊢ M ⊆ U, univ_trans 1 2.
05. 1, 2 ⊢ map (id M) M U, map_rng_weaken 3 4.
06. 1, 2 ⊢ ⋃(img (id M) M) ∈ U, univ_closed_family_union 1 2 5.
07. ⊢ M ⊆ M, incl_refl.
08. ⊢ img (id M) M = M, id_img 7.
09. 1, 2 ⊢ ⋃M ∈ U, eq_subst 8 6, P x ↔ ⋃x ∈ U.
univ_closed_Union. ⊢ univ U → M ∈ U → ⋃M ∈ U, subj_intro_ii 9.

01. 1 ⊢ set A, hypo.
02. 1 ⊢ ∃U. set U ∧ univ U ∧ A ∈ U, universes 1.
03. 3 ⊢ set U ∧ univ U ∧ A ∈ U, hypo.
04. 3 ⊢ set U, conj_elimll 3.
05. 3 ⊢ univ U, conj_elimlr 3.
06. 3 ⊢ A ∈ U, conj_elimr 3.
07. 3 ⊢ univ U ∧ A ∈ U, conj_intro 5 6.
08. 3 ⊢ U ∈ {U | univ U ∧ A ∈ U}, comp_intro 4 7.
09. 3 ⊢ least_univ A ⊆ U, Intersection_is_lower_bound least_univ_eq 8.
10. 3 ⊢ set (least_univ A), subset 9 4.
11. 1 ⊢ set (least_univ A), ex_elim 2 10.

12. 12 ⊢ x ∈ least_univ A, hypo.
13. 12 ⊢ x ∈ ⋂{U | univ U ∧ A ∈ U},
  eq_subst least_univ_eq 12, P t ↔ x ∈ t.
14. 14 ⊢ y ∈ x, hypo.
15. 15 ⊢ U ∈ {U | univ U ∧ A ∈ U}, hypo.
16. 15 ⊢ univ U ∧ A ∈ U, comp_elim 15.
17. 15 ⊢ univ U, conj_eliml 16.
18. 15 ⊢ A ∈ U, conj_elimr 16.
19. 12, 15 ⊢ x ∈ U, Intersection_elim 13 15.
20. 12, 15 ⊢ x ⊆ U, univ_trans 17 19.
21. 12, 14, 15 ⊢ y ∈ U, incl_elim 20 14.
22. 12, 14 ⊢ U ∈ {U | univ U ∧ A ∈ U} → y ∈ U, subj_intro 21.
23. 12, 14 ⊢ ∀U. U ∈ {U | univ U ∧ A ∈ U} → y ∈ U, uq_intro 22.
24. 14 ⊢ set y, set_intro 14.
25. 12, 14 ⊢ y ∈ ⋂{U | univ U ∧ A ∈ U}, Intersection_intro 24 23.
26. 12, 14 ⊢ y ∈ least_univ A,
  eq_subst_rev least_univ_eq 25, P t ↔ y ∈ t.
27. 12 ⊢ y ∈ x → y ∈ least_univ A, subj_intro 26.
28. 12 ⊢ ∀y. y ∈ x → y ∈ least_univ A, uq_intro 27.
29. 12 ⊢ x ⊆ least_univ A, incl_intro 28.
30. ⊢ x ∈ least_univ A → x ⊆ least_univ A, subj_intro 29.
31. ⊢ ∀x. x ∈ least_univ A → x ⊆ least_univ A, uq_intro 30.

32. 12, 15 ⊢ power x ∈ U, univ_closed_power 17 19.
33. 12 ⊢ U ∈ {U | univ U ∧ A ∈ U} → power x ∈ U, subj_intro 32.
34. 12 ⊢ ∀U. U ∈ {U | univ U ∧ A ∈ U} → power x ∈ U, uq_intro 33.
35. 12 ⊢ set x, set_intro 12.
36. 12 ⊢ set (power x), power 35.
37. 12 ⊢ power x ∈ ⋂{U | univ U ∧ A ∈ U}, Intersection_intro 36 34.
38. 12 ⊢ power x ∈ least_univ A,
  eq_subst_rev least_univ_eq 37, P t ↔ power x ∈ t.
39. ⊢ x ∈ least_univ A → power x ∈ least_univ A, subj_intro 38.
40. ⊢ ∀x. x ∈ least_univ A → power x ∈ least_univ A, uq_intro 39.

41. 41 ⊢ I ∈ least_univ A, hypo.
42. 41 ⊢ I ∈ ⋂{U | univ U ∧ A ∈ U},
  eq_subst least_univ_eq 41, P t ↔ I ∈ t.
43. 43 ⊢ map f I (least_univ A), hypo.
44. 41, 15 ⊢ I ∈ U, Intersection_elim 42 15.
45. 15 ⊢ least_univ A ⊆ U, Intersection_is_lower_bound least_univ_eq 15.
46. 43, 15 ⊢ map f I U, map_rng_weaken 43 45.
47. 41, 43, 15 ⊢ ⋃(img f I) ∈ U, univ_closed_family_union 17 44 46.
48. 41, 43 ⊢ U ∈ {U | univ U ∧ A ∈ U} → ⋃(img f I) ∈ U, subj_intro 47.
49. 41, 43 ⊢ ∀U. U ∈ {U | univ U ∧ A ∈ U} → ⋃(img f I) ∈ U, uq_intro 48.
50. 43 ⊢ function f ∧ dom f = I ∧ rng f ⊆ least_univ A, map_unfold 43.
51. 43 ⊢ rng f ⊆ least_univ A, conj_elimr 50.
52. ⊢ img f I ⊆ rng f, img_incl_in_rng.
53. 43 ⊢ img f I ⊆ least_univ A, incl_trans 52 51.
54. 1, 43 ⊢ set (img f I), subset 53 11.
55. 1, 43 ⊢ set (⋃(img f I)), union 54.
56. 1, 41, 43 ⊢ ⋃(img f I) ∈ ⋂{U | univ U ∧ A ∈ U},
  Intersection_intro 55 49.
57. 1, 41, 43 ⊢ ⋃(img f I) ∈ least_univ A,
  eq_subst_rev least_univ_eq 56, P t ↔ ⋃(img f I) ∈ t.
58. 1 ⊢ I ∈ least_univ A → map f I (least_univ A) →
  ⋃(img f I) ∈ least_univ A, subj_intro_ii 57.
59. 1 ⊢ ∀f. I ∈ least_univ A → map f I (least_univ A) →
  ⋃(img f I) ∈ least_univ A, uq_intro 58.
60. 1 ⊢ ∀I. ∀f. I ∈ least_univ A → map f I (least_univ A) →
  ⋃(img f I) ∈ least_univ A, uq_intro 59.

61. 15 ⊢ ∅ ∈ U, univ_contains_empty_set 17.
62. ⊢ U ∈ {U | univ U ∧ A ∈ U} → ∅ ∈ U, subj_intro 61.
63. ⊢ ∀U. U ∈ {U | univ U ∧ A ∈ U} → ∅ ∈ U, uq_intro 62.
64. ⊢ ∅ ∈ ⋂{U | univ U ∧ A ∈ U},
  Intersection_intro empty_set_is_set 63.
65. ⊢ ∅ ∈ least_univ A, eq_subst_rev least_univ_eq 64, P t ↔ ∅ ∈ t.

66. ⊢ ∅ ∈ least_univ A ∧
  (∀x. x ∈ least_univ A → x ⊆ least_univ A), conj_intro 65 31.
67. ⊢ ∅ ∈ least_univ A ∧
  (∀x. x ∈ least_univ A → x ⊆ least_univ A) ∧
  (∀x. x ∈ least_univ A → power x ∈ least_univ A), conj_intro 66 40.
68. 1 ⊢ ∅ ∈ least_univ A ∧
  (∀x. x ∈ least_univ A → x ⊆ least_univ A) ∧
  (∀x. x ∈ least_univ A → power x ∈ least_univ A) ∧
  (∀I. ∀f. I ∈ least_univ A → map f I (least_univ A) →
    ⋃(img f I) ∈ least_univ A), conj_intro 67 60.
69. ⊢ univ (least_univ A) ↔ ∅ ∈ least_univ A ∧
  (∀x. x ∈ least_univ A → x ⊆ least_univ A) ∧
  (∀x. x ∈ least_univ A → power x ∈ least_univ A) ∧
  (∀I. ∀x. I ∈ least_univ A → map x I (least_univ A) →
    ⋃(img x I) ∈ least_univ A), univ_equi.
70. 1 ⊢ univ (least_univ A), rsubj_elim 69 68.
71. 1 ⊢ set (least_univ A) ∧ univ (least_univ A), conj_intro 11 70.
least_univ_is_univ. ⊢ set A →
  set (least_univ A) ∧ univ (least_univ A), subj_intro 71.

01. ⊢ set (least_univ onat) ∧ univ (least_univ onat),
  least_univ_is_univ onat_is_set.
02. ⊢ set (least_univ onat), conj_eliml 1.
Univ_is_set. ⊢ set Univ, eq_subst_rev Univ_eq 2, P t ↔ set t.

01. ⊢ set (least_univ onat) ∧ univ (least_univ onat),
  least_univ_is_univ onat_is_set.
02. ⊢ univ (least_univ onat), conj_elimr 1.
Univ_is_univ. ⊢ univ Univ, eq_subst_rev Univ_eq 2, P t ↔ univ t.

01. 1 ⊢ univ U, hypo.
02. 2 ⊢ x ∈ U, hypo.
03. 1, 2 ⊢ power x ∈ U, univ_closed_power 1 2.
04. ⊢ U ∩ power x ⊆ power x, intersection_incl_right.
05. 1, 2 ⊢ U ∩ power x ∈ U, univ_closed_subset 1 4 3.
truncated_power_closed. ⊢ univ U → x ∈ U → U ∩ power x ∈ U,
  subj_intro_ii 5.

01. 1 ⊢ univ U, hypo.
02. 2 ⊢ x ∈ U, hypo.
03. 1, 2 ⊢ x ⊆ U, univ_trans 1 2.
04. 2 ⊢ set x, set_intro 2.
05. 1, 2 ⊢ x ∈ power U, power_intro 4 3.
06. 1 ⊢ x ∈ U → x ∈ power U, subj_intro 5.
07. 1 ⊢ ∀x. x ∈ U → x ∈ power U, uq_intro 6.
08. 1 ⊢ U ⊆ power U, incl_intro 7.
09. 1 ⊢ U ∩ power U = U, intersection_from_incl 8.
truncated_power_idem. ⊢ univ U → U ∩ power U = U, subj_intro 9.

(*/note univ_equi Defines what is considered to be the Grothendieck
universe. Their existence is assured by the
[[universes|axiom of universes]]. *)

(*/note least_univ_eq The least [[univ_equi|Grothendieck universe]]
that contains A. *)

(*/note Univ_eq The universal set, defined as the least
[[univ_equi|Grothendieck universe]] that contains the
[[onat_eq|natural numbers]], see the [[least_univ_eq|least_univ]]
operation. Basically this means, it behaves almost exactly like
[[UnivCl_eq|UnivCl]], but it is a [[set_equi|set]] rather than
a proper class. Theorem [[Univ_is_univ]] shows that it is
well-defined, the proof depends on the
[[universes|axiom of universes]]. *)

(*/note universes The axiom of universes. It states that each
[[set_equi|set]] is a member of some
[[univ_equi|Grothendieck universe]]. *)
