
(*/chapter Power sets *)

01. 1 ⊢ set x, hypo.
02. 2 ⊢ x ⊆ y, hypo.
03. 1, 2 ⊢ x ∈ {x | x ⊆ y}, comp_intro 1 2.
04. ⊢ power y = {x | x ⊆ y}, power_eq.
05. 1, 2 ⊢ x ∈ power y, eq_subst_rev 4 3, P u ↔ x ∈ u.
power_intro. ⊢ set x → x ⊆ y → x ∈ power y, subj_intro_ii 5.

01. 1 ⊢ x ∈ power M, hypo.
02. ⊢ power M = {A | A ⊆ M}, power_eq.
03. 1 ⊢ x ∈ {A | A ⊆ M}, eq_subst 2 1, P u ↔ x ∈ u.
04. 1 ⊢ x ⊆ M, comp_elim 3.
power_elim. ⊢ x ∈ power M → x ⊆ M, subj_intro 4.

01. 1 ⊢ x ∈ UnivCl, hypo.
02. 1 ⊢ set x, set_intro 1.
03. ⊢ x ⊆ UnivCl, UnivCl_is_greatest.
04. 1 ⊢ x ∈ power UnivCl, power_intro 2 3.
05. ⊢ x ∈ UnivCl → x ∈ power UnivCl, subj_intro 4.
06. ⊢ ∀x. x ∈ UnivCl → x ∈ power UnivCl, uq_intro 5.
07. ⊢ UnivCl ⊆ power UnivCl, incl_intro 6.
08. ⊢ power UnivCl ⊆ UnivCl, UnivCl_is_greatest.
power_UnivCl. ⊢ power UnivCl = UnivCl, incl_antisym 8 7.

01. 1 ⊢ t ∈ A × B, hypo.
02. 1 ⊢ ∃x. ∃y. x ∈ A ∧ y ∈ B ∧ t = (x, y), prod_elim 1.
03. 3 ⊢ ∃y. x ∈ A ∧ y ∈ B ∧ t = (x, y), hypo.
04. 4 ⊢ x ∈ A ∧ y ∈ B ∧ t = (x, y), hypo.
05. 4 ⊢ t = (x, y), conj_elimr 4.
06. 4 ⊢ t = {{x}, {x, y}}, eq_subst pair_eq 5, P u ↔ t = u.
07. 4 ⊢ x ∈ A ∧ y ∈ B, conj_eliml 4.
08. 4 ⊢ x ∈ A, conj_eliml 7.
09. 4 ⊢ y ∈ B, conj_elimr 7.
10. 4 ⊢ {x} ⊆ A, sg_incl_in 8.
11. 4 ⊢ {y} ⊆ B, sg_incl_in 9.
12. 4 ⊢ {x, y} ⊆ A ∪ B, union_incl_in_union 10 11.
13. ⊢ A ⊆ A ∪ B, union_incl_left.
14. 4 ⊢ {x} ⊆ A ∪ B, incl_trans 10 13.
15. 4 ⊢ set x, set_intro 8.
16. 4 ⊢ set y, set_intro 9.
17. 4 ⊢ set {x}, set_sg 15.
18. 4 ⊢ set {y}, set_sg 16.
19. 4 ⊢ set {x, y}, set_union 17 18.
20. 4 ⊢ {x} ∈ power (A ∪ B), power_intro 17 14.
21. 4 ⊢ {x, y} ∈ power (A ∪ B), power_intro 19 12.
22. 4 ⊢ {{x}} ⊆ power (A ∪ B), sg_incl_in 20.
23. 4 ⊢ {{x, y}} ⊆ power (A ∪ B), sg_incl_in 21.
24. 4 ⊢ {{x}, {x, y}} ⊆ power (A ∪ B), union_incl_in 22 23.
25. 4 ⊢ set {{x}}, set_sg 17.
26. 4 ⊢ set {{x, y}}, set_sg 19.
27. 4 ⊢ set {{x}, {x, y}}, set_union 25 26.
28. 4 ⊢ {{x}, {x, y}} ∈ power (power (A ∪ B)), power_intro 27 24.
29. 4 ⊢ t ∈ power (power (A ∪ B)), eq_subst_rev 6 28,
  P u ↔ u ∈ power (power (A ∪ B)).
30. 3 ⊢ t ∈ power (power (A ∪ B)), ex_elim 3 29.
31. 1 ⊢ t ∈ power (power (A ∪ B)), ex_elim 2 30.
32. ⊢ t ∈ A × B → t ∈ power (power (A ∪ B)), subj_intro 31.
33. ⊢ ∀t. t ∈ A × B → t ∈ power (power (A ∪ B)), uq_intro 32.
prod_incl_in_power. ⊢ A × B ⊆ power (power (A ∪ B)), incl_intro 33.

01. 1 ⊢ set A, hypo.
02. 2 ⊢ set B, hypo.
03. 1, 2 ⊢ set (A ∪ B), set_union 1 2.
04. 1, 2 ⊢ set (power (A ∪ B)), power 3.
05. 1, 2 ⊢ set (power (power (A ∪ B))), power 4.
06. 1, 2 ⊢ set (A × B), subset prod_incl_in_power 5.
set_prod. ⊢ set A → set B → set (A × B), subj_intro_ii 6.

01. 1 ⊢ set X, hypo.
02. ⊢ {x | x ∈ X ∧ P x} ⊆ X, sep_is_subclass.
03. 1 ⊢ set {x | x ∈ X ∧ P x}, subset 2 1.
04. 1 ⊢ {x | x ∈ X ∧ P x} ∈ power X, power_intro 3 2.
sep_in_power. ⊢ set X → {x | x ∈ X ∧ P x} ∈ power X, subj_intro 4.

01. 1 ⊢ x ∈ power ∅, hypo.
02. 1 ⊢ x ⊆ ∅, power_elim 1.
03. ⊢ ∅ ⊆ x, empty_set_is_least.
04. 1 ⊢ x = ∅, incl_antisym 2 3.
05. 1 ⊢ x ∈ {∅}, sg_intro empty_set_is_set 4.
06. ⊢ x ∈ power ∅ → x ∈ {∅}, subj_intro 5.
07. 7 ⊢ x ∈ {∅}, hypo.
08. 7 ⊢ x = ∅, sg_elim empty_set_is_set 7.
09. 7 ⊢ x ⊆ ∅, incl_from_eq 8.
10. 7 ⊢ set x, set_intro 7.
11. 7 ⊢ x ∈ power ∅, power_intro 10 9.
12. ⊢ x ∈ {∅} → x ∈ power ∅, subj_intro 11.
13. ⊢ x ∈ power ∅ ↔ x ∈ {∅}, bij_intro 6 12.
14. ⊢ ∀x. x ∈ power ∅ ↔ x ∈ {∅}, uq_intro 13.
power_empty_set. ⊢ power ∅ = {∅}, ext 14.

01. 1 ⊢ set a, hypo.
02. 2 ⊢ x ∈ power {a}, hypo.
03. 2 ⊢ x ⊆ {a}, power_elim 2.
04. 1, 2 ⊢ x = ∅ ∨ x = {a}, subsets_of_sg 1 3.
05. 5 ⊢ x = ∅, hypo.
06. 5 ⊢ x ∈ {∅}, sg_intro empty_set_is_set 5.
07. 5 ⊢ x ∈ {∅, {a}}, union_introl 6.
08. 8 ⊢ x = {a}, hypo.
09. 1 ⊢ set {a}, set_sg 1.
10. 1, 8 ⊢ x ∈ {{a}}, sg_intro 9 8.
11. 1, 8 ⊢ x ∈ {∅, {a}}, union_intror 10.
12. 1, 2 ⊢ x ∈ {∅, {a}}, disj_elim 4 7 11.
13. 1 ⊢ x ∈ power {a} → x ∈ {∅, {a}}, subj_intro 12.
14. 14 ⊢ x ∈ {∅, {a}}, hypo.
15. 14 ⊢ x ∈ {∅} ∨ x ∈ {{a}}, union_elim 14.
16. 16 ⊢ x ∈ {∅}, hypo.
17. 16 ⊢ x = ∅, sg_elim empty_set_is_set 16.
18. ⊢ ∅ ⊆ {a}, empty_set_is_least.
19. 16 ⊢ x ⊆ {a}, eq_subst_rev 17 18, P u ↔ u ⊆ {a}.
20. 20 ⊢ x ∈ {{a}}, hypo.
21. 1, 20 ⊢ x = {a}, sg_elim 9 20.
22. ⊢ {a} ⊆ {a}, incl_refl.
23. 1, 20 ⊢ x ⊆ {a}, eq_subst_rev 21 22, P u ↔ u ⊆ {a}.
24. 1, 14 ⊢ x ⊆ {a}, disj_elim 15 19 23.
25. 14 ⊢ set x, set_intro 14.
26. 1, 14 ⊢ x ∈ power {a}, power_intro 25 24.
27. 1 ⊢ x ∈ {∅, {a}} → x ∈ power {a}, subj_intro 26.
28. 1 ⊢ x ∈ power {a} ↔ x ∈ {∅, {a}}, bij_intro 13 27.
29. 1 ⊢ ∀x. x ∈ power {a} ↔ x ∈ {∅, {a}}, uq_intro 28.
30. 1 ⊢ power {a} = {∅, {a}}, ext 29.
power_sg. ⊢ set a → power {a} = {∅, {a}}, subj_intro 30.

01. 1 ⊢ set x, hypo.
02. 2 ⊢ u ∈ {x}, hypo.
03. 1, 2 ⊢ u = x, sg_elim 1 2.
04. ⊢ x ⊆ x, incl_refl.
05. 1 ⊢ x ∈ power x, power_intro 1 4.
06. 1, 2 ⊢ u ∈ power x, eq_subst_rev 3 5, P u ↔ u ∈ power x.
07. 1 ⊢ u ∈ {x} → u ∈ power x, subj_intro 6.
08. 1 ⊢ ∀u. u ∈ {x} → u ∈ power x, uq_intro 7.
09. 1 ⊢ {x} ⊆ power x, incl_intro 8.
sg_incl_in_power. ⊢ set x → {x} ⊆ power x, subj_intro 9.

01. 1 ⊢ function f, hypo.
02. 2 ⊢ set (dom f), hypo.
03. 1, 2 ⊢ set (rng f), substitution 1 2.
04. 1 ⊢ relation f, fn_is_rel 1.
05. ⊢ dom f ⊆ dom f, incl_refl.
06. ⊢ rng f ⊆ rng f, incl_refl.
07. 1 ⊢ f ⊆ dom f × rng f, relation_incl_prod 4 5 6.
08. 1, 2 ⊢ set (dom f × rng f), set_prod 2 3.
09. 1, 2 ⊢ set f, subset 7 8.
function_is_set. ⊢ function f → set (dom f) → set f, subj_intro_ii 9.

01. 1 ⊢ map f X Y, hypo.
02. 2 ⊢ set X, hypo.
03. 1 ⊢ function f ∧ dom f = X ∧ rng f ⊆ Y, map_unfold 1.
04. 1 ⊢ function f, conj_elimll 3.
05. 1 ⊢ dom f = X, conj_elimlr 3.
06. 1, 2 ⊢ set (dom f), eq_subst_rev 5 2.
07. 1, 2 ⊢ set f, function_is_set 4 6.
map_is_set. ⊢ map f X Y → set X → set f, subj_intro_ii 7.

01. 1 ⊢ map f X Y, hypo.
02. 2 ⊢ set X, hypo.
03. 3 ⊢ set Y, hypo.
04. 2, 3 ⊢ set (X × Y), set_prod 2 3.
05. 1 ⊢ f ⊆ X × Y, map_incl_in_prod 1.
06. 1, 2, 3 ⊢ set f, subset 5 4.
map_is_set_dom_cod. ⊢ map f X Y → set X → set Y → set f,
  subj_intro_iii 6.
