
(*/chapter Orders *)

(*/section General concepts *)

is_max_equi. ⊢ is_max R y A ↔ y ∈ A ∧ ∀x. x ∈ A → (x, y) ∈ R, def.
is_min_equi. ⊢ is_min R y A ↔ y ∈ A ∧ ∀x. x ∈ A → (y, x) ∈ R, def.

is_supremum_equi. ⊢ is_supremum R M y A ↔
  (∀x. x ∈ A → (x, y) ∈ R) ∧
  (∀b. b ∈ M → (∀x. x ∈ A → (x, b) ∈ R) → (y, b) ∈ R), def.

is_infimum_equi. ⊢ is_infimum R M y A ↔
  (∀x. x ∈ A → (y, x) ∈ R) ∧
  (∀b. b ∈ M → (∀x. x ∈ A → (b, x) ∈ R) → (b, y) ∈ R), def.

is_supremum_unfold. ⊢ is_supremum R M y A →
  (∀x. x ∈ A → (x, y) ∈ R) ∧
  (∀b. b ∈ M → (∀x. x ∈ A → (x, b) ∈ R) → (y, b) ∈ R),
  bij_eliml is_supremum_equi.

01. 1 ⊢ ∀x. x ∈ A → (x, y) ∈ R, hypo.
02. 2 ⊢ ∀b. b ∈ M → (∀x. x ∈ A → (x, b) ∈ R) → (y, b) ∈ R, hypo.
03. 1, 2 ⊢ (∀x. x ∈ A → (x, y) ∈ R) ∧
  (∀b. b ∈ M → (∀x. x ∈ A → (x, b) ∈ R) → (y, b) ∈ R), conj_intro 1 2.
05. 1, 2 ⊢ is_supremum R M y A, rsubj_elim is_supremum_equi 3.
is_supremum_intro. ⊢ (∀x. x ∈ A → (x, y) ∈ R) →
  (∀b. b ∈ M → (∀x. x ∈ A → (x, b) ∈ R) → (y, b) ∈ R) →
  is_supremum R M y A, subj_intro_ii 5.

01. 1 ⊢ A ⊆ B, hypo.
02. 2 ⊢ b ∈ M, hypo.
03. 3 ⊢ is_supremum R M a A, hypo.
04. 4 ⊢ is_supremum R M b B, hypo.
05. 3 ⊢ (∀x. x ∈ A → (x, a) ∈ R) ∧
  (∀z. z ∈ M → (∀x. x ∈ A → (x, z) ∈ R) → (a, z) ∈ R),
  is_supremum_unfold 3.
06. 4 ⊢ (∀x. x ∈ B → (x, b) ∈ R) ∧
  (∀z. z ∈ M → (∀x. x ∈ B → (x, z) ∈ R) → (b, z) ∈ R),
  is_supremum_unfold 4.
07. 7 ⊢ x ∈ A, hypo.
08. 1, 7 ⊢ x ∈ B, incl_elim 1 7.
09. 4 ⊢ ∀x. x ∈ B → (x, b) ∈ R, conj_eliml 6.
10. 1, 4, 7 ⊢ (x, b) ∈ R, uq_bounded_elim 9 8.
11. 1, 4 ⊢ x ∈ A → (x, b) ∈ R, subj_intro 10.
12. 1, 4 ⊢ ∀x. x ∈ A → (x, b) ∈ R, uq_intro 11.
13. 3 ⊢ ∀z. z ∈ M → (∀x. x ∈ A → (x, z) ∈ R) → (a, z) ∈ R,
  conj_elimr 5.
14. 2, 3 ⊢ (∀x. x ∈ A → (x, b) ∈ R) → (a, b) ∈ R,
  uq_bounded_elim 13 2.
15. 1, 2, 3, 4 ⊢ (a, b) ∈ R, subj_elim 14 12.
supremum_is_monotonic. ⊢ A ⊆ B → b ∈ M →
  is_supremum R M a A → is_supremum R M b B →
  (a, b) ∈ R, subj_intro_iv 15.


(*/section Partial orders *)

po_equi. ⊢ partial_order R M ↔
  (∀x. x ∈ M → (x, x) ∈ R) ∧
  (∀x. ∀y. x ∈ M → y ∈ M → (x, y) ∈ R → (y, x) ∈ R → x = y) ∧
  (∀x. ∀y. ∀z. x ∈ M → y ∈ M → z ∈ M →
    (x, y) ∈ R → (y, z) ∈ R → (x, z) ∈ R), def.

01. 1 ⊢ partial_order R M, hypo.
02. 1 ⊢ ∀x. x ∈ M → (x, x) ∈ R, lsubj_conj_elimll po_equi 1.
03. 1 ⊢ x ∈ M → (x, x) ∈ R, uq_elim 2.
po_refl. ⊢ partial_order R M → x ∈ M → (x, x) ∈ R, subj_intro 3.

01. 1 ⊢ partial_order R M, hypo.
02. 1 ⊢ ∀x. ∀y. x ∈ M → y ∈ M → (x, y) ∈ R → (y, x) ∈ R → x = y,
  lsubj_conj_elimlr po_equi 1.
03. 1 ⊢ ∀y. x ∈ M → y ∈ M → (x, y) ∈ R → (y, x) ∈ R → x = y, uq_elim 2.
04. 1 ⊢ x ∈ M → y ∈ M → (x, y) ∈ R → (y, x) ∈ R → x = y, uq_elim 3.
po_antisym. ⊢ partial_order R M → x ∈ M → y ∈ M →
  (x, y) ∈ R → (y, x) ∈ R → x = y, subj_intro 4.

01. 1 ⊢ partial_order R M, hypo.
02. 1 ⊢ ∀x. ∀y. ∀z. x ∈ M → y ∈ M → z ∈ M →
  (x, y) ∈ R → (y, z) ∈ R → (x, z) ∈ R, lsubj_conj_elimr po_equi 1.
03. 1 ⊢ ∀y. ∀z. x ∈ M → y ∈ M → z ∈ M →
  (x, y) ∈ R → (y, z) ∈ R → (x, z) ∈ R, uq_elim 2.
04. 1 ⊢ ∀z. x ∈ M → y ∈ M → z ∈ M →
  (x, y) ∈ R → (y, z) ∈ R → (x, z) ∈ R, uq_elim 3.
05. 1 ⊢ x ∈ M → y ∈ M → z ∈ M →
  (x, y) ∈ R → (y, z) ∈ R → (x, z) ∈ R, uq_elim 4.
po_trans. ⊢ partial_order R M → x ∈ M → y ∈ M → z ∈ M →
  (x, y) ∈ R → (y, z) ∈ R → (x, z) ∈ R, subj_intro 5.

01. 1 ⊢ is_supremum R M a A, hypo.
02. 2 ⊢ is_supremum R M b B, hypo.
03. 3 ⊢ is_supremum R M c (A ∪ B), hypo.
04. 4 ⊢ a ∈ M, hypo.
05. 5 ⊢ b ∈ M, hypo.
06. 6 ⊢ c ∈ M, hypo.
07. 4 ⊢ set a, set_intro 4.
08. 5 ⊢ set b, set_intro 5.
09. 1 ⊢ (∀x. x ∈ A → (x, a) ∈ R) ∧
  (∀z. z ∈ M → (∀x. x ∈ A → (x, z) ∈ R) → (a, z) ∈ R),
  is_supremum_unfold 1.
10. 2 ⊢ (∀x. x ∈ B → (x, b) ∈ R) ∧
  (∀z. z ∈ M → (∀x. x ∈ B → (x, z) ∈ R) → (b, z) ∈ R),
  is_supremum_unfold 2.
11. 3 ⊢ (∀x. x ∈ A ∪ B → (x, c) ∈ R) ∧
  (∀z. z ∈ M → (∀x. x ∈ A ∪ B → (x, z) ∈ R) → (c, z) ∈ R),
  is_supremum_unfold 3.

12. 12 ⊢ x ∈ {a, b}, hypo.
13. 4, 5, 12 ⊢ x = a ∨ x = b, pair_set_elim 7 8 12.
14. 14 ⊢ x = a, hypo.
15. ⊢ A ⊆ A ∪ B, union_incl_left.
16. 1, 3, 6 ⊢ (a, c) ∈ R, supremum_is_monotonic 15 6 1 3.
17. 1, 3, 6, 14 ⊢ (x, c) ∈ R, eq_subst_rev 14 16.
18. 18 ⊢ x = b, hypo.
19. ⊢ B ⊆ A ∪ B, union_incl_right.
20. 2, 3, 6 ⊢ (b, c) ∈ R, supremum_is_monotonic 19 6 2 3.
21. 2, 3, 6, 18 ⊢ (x, c) ∈ R, eq_subst_rev 18 20.
22. 1, 2, 3, 4, 5, 6, 12 ⊢ (x, c) ∈ R, disj_elim 13 17 21.
23. 1, 2, 3, 4, 5, 6 ⊢ x ∈ {a, b} → (x, c) ∈ R, subj_intro 22.
24. 1, 2, 3, 4, 5, 6 ⊢ ∀x. x ∈ {a, b} → (x, c) ∈ R, uq_intro 23.

25. 25 ⊢ partial_order R M, hypo.
26. 26 ⊢ A ⊆ M, hypo.
27. 27 ⊢ B ⊆ M, hypo.
28. 28 ⊢ x ∈ M, hypo.
29. 29 ⊢ ∀z. z ∈ {a, b} → (z, x) ∈ R, hypo.
30. 30 ⊢ z ∈ A ∪ B, hypo.
31. 30 ⊢ z ∈ A ∨ z ∈ B, union_elim 30.

32. 32 ⊢ z ∈ A, hypo.
33. 1 ⊢ ∀x. x ∈ A → (x, a) ∈ R, conj_eliml 9.
34. 1, 32 ⊢ (z, a) ∈ R, uq_bounded_elim 33 32.
35. 4 ⊢ a ∈ {a, b}, in21 7.
36. 4, 29 ⊢ (a, x) ∈ R, uq_bounded_elim 29 35.
37. 26, 32 ⊢ z ∈ M, incl_elim 26 32.
38. 1, 4, 25, 26, 28, 29, 32 ⊢ (z, x) ∈ R, po_trans 25 37 4 28 34 36.

39. 39 ⊢ z ∈ B, hypo.
40. 2 ⊢ ∀x. x ∈ B → (x, b) ∈ R, conj_eliml 10.
41. 2, 39 ⊢ (z, b) ∈ R, uq_bounded_elim 40 39.
42. 5 ⊢ b ∈ {a, b}, in22 8.
43. 5, 29 ⊢ (b, x) ∈ R, uq_bounded_elim 29 42.
44. 27, 39 ⊢ z ∈ M, incl_elim 27 39.
45. 2, 5, 25, 27, 28, 29, 39 ⊢ (z, x) ∈ R, po_trans 25 44 5 28 41 43.

46. 1, 2, 4, 5, 25, 26, 27, 28, 29, 30 ⊢ (z, x) ∈ R, disj_elim 31 38 45.
47. 1, 2, 4, 5, 25, 26, 27, 28, 29 ⊢ z ∈ A ∪ B → (z, x) ∈ R, subj_intro 46.
48. 1, 2, 4, 5, 25, 26, 27, 28, 29 ⊢ ∀z. z ∈ A ∪ B → (z, x) ∈ R, uq_intro 47.
49. 3 ⊢ ∀x. x ∈ M → (∀z. z ∈ A ∪ B → (z, x) ∈ R) → (c, x) ∈ R, conj_elimr 11.
50. 3, 28 ⊢ (∀z. z ∈ A ∪ B → (z, x) ∈ R) → (c, x) ∈ R, uq_bounded_elim 49 28.
51. 1, 2, 3, 4, 5, 25, 26, 27, 28, 29 ⊢ (c, x) ∈ R, subj_elim 50 48.
52. 1, 2, 3, 4, 5, 25, 26, 27 ⊢ x ∈ M →
  (∀z. z ∈ {a, b} → (z, x) ∈ R) → (c, x) ∈ R, subj_intro_ii 51.
53. 1, 2, 3, 4, 5, 25, 26, 27 ⊢ ∀x. x ∈ M →
  (∀z. z ∈ {a, b} → (z, x) ∈ R) → (c, x) ∈ R, uq_intro 52.

54. 25, 26, 27, 4, 5, 6, 1, 2, 3 ⊢ is_supremum R M c {a, b},
  is_supremum_intro 24 53.
55. 25, 26, 27, 4, 5, 6 ⊢ is_supremum R M a A → is_supremum R M b B →
  is_supremum R M c (A ∪ B) → is_supremum R M c {a, b},
  subj_intro_iii 54.
56. 25, 26, 27 ⊢ a ∈ M → b ∈ M → c ∈ M →
  is_supremum R M a A → is_supremum R M b B →
  is_supremum R M c (A ∪ B) → is_supremum R M c {a, b},
  subj_intro_iii 55.
supremum_union. ⊢ partial_order R M → A ⊆ M → B ⊆ M →
  a ∈ M → b ∈ M → c ∈ M →
  is_supremum R M a A → is_supremum R M b B →
  is_supremum R M c (A ∪ B) → is_supremum R M c {a, b},
  subj_intro_iii 56.


(*/section Power set lattice *)

begin
let M = {x | x ⊆ a ∧ app f x ⊆ x}.
let w = ⋂M.
01. 1 ⊢ set a, hypo.
02. 2 ⊢ map f (power a) (power a), hypo.
03. 3 ⊢ ∀x. ∀y. y ⊆ a → x ⊆ y → app f x ⊆ app f y, hypo.
04. 4 ⊢ x ∈ M, hypo.
05. ⊢ w = w, eq_refl.
06. 4 ⊢ w ⊆ x, Intersection_is_lower_bound 5 4.
07. 4 ⊢ x ⊆ a ∧ app f x ⊆ x, comp_elim 4.
08. 4 ⊢ x ⊆ a, conj_eliml 7.
09. 3 ⊢ ∀y. y ⊆ a → w ⊆ y → app f w ⊆ app f y, uq_elim 3.
10. 3, 4 ⊢ w ⊆ x → app f w ⊆ app f x, uq_bounded_elim 9 8.
11. 3, 4 ⊢ app f w ⊆ app f x, subj_elim 10 6.
12. 4 ⊢ app f x ⊆ x, conj_elimr 7.
13. 3, 4 ⊢ app f w ⊆ x, incl_trans 11 12.
14. 3 ⊢ x ∈ M → app f w ⊆ x, subj_intro 13.
15. 3 ⊢ ∀x. x ∈ M → app f w ⊆ x, uq_intro 14.
16. 3 ⊢ app f w ⊆ w, Intersection_is_infimum 15.
17. ⊢ a ⊆ a, incl_refl.
18. 1 ⊢ a ∈ power a, power_intro 1 17.
19. 1, 2 ⊢ app f a ∈ power a, map_app_in_cod 2 18.
20. 1, 2 ⊢ app f a ⊆ a, power_elim 19.
21. 1, 2 ⊢ a ⊆ a ∧ app f a ⊆ a, conj_intro 17 20.
22. 1, 2 ⊢ a ∈ M, comp_intro 1 21, P x ↔ x ⊆ a ∧ app f x ⊆ x.
23. 1, 2 ⊢ w ⊆ a, Intersection_is_lower_bound 5 22.
24. 3 ⊢ ∀y. y ⊆ a → app f w ⊆ y →
  app f (app f w) ⊆ app f y, uq_elim 3.
25. 1, 2, 3 ⊢ app f w ⊆ w →
  app f (app f w) ⊆ app f w, uq_bounded_elim 24 23.
26. 1, 2, 3 ⊢ app f (app f w) ⊆ app f w, subj_elim 25 16.
27. 1, 2, 3 ⊢ app f w ⊆ a, incl_trans 16 23.
28. 1, 2, 3 ⊢ set (app f w), subset 27 1.
29. 1, 2, 3 ⊢ app f w ⊆ a ∧
  app f (app f w) ⊆ app f w, conj_intro 27 26.
30. 1, 2, 3 ⊢ app f w ∈ M,
  comp_intro 28 29, P x ↔ x ⊆ a ∧ app f x ⊆ x.
31. 1, 2, 3 ⊢ w ⊆ app f w, Intersection_is_lower_bound 5 30.
32. 1, 2, 3 ⊢ app f w = w, incl_antisym 16 31.
33. 33 ⊢ x ⊆ a, hypo.
34. 34 ⊢ app f x = x, hypo.
35. 34 ⊢ app f x ⊆ x, incl_from_eq 34.
36. 33, 34 ⊢ x ⊆ a ∧ app f x ⊆ x, conj_intro 33 35.
37. 1, 33 ⊢ set x, subset 33 1.
38. 1, 33, 34 ⊢ x ∈ M, comp_intro 37 36.
39. 1, 33, 34 ⊢ w ⊆ x, Intersection_is_lower_bound 5 38.
40. 1 ⊢ x ⊆ a → app f x = x → w ⊆ x, subj_intro_ii 39.
41. 1 ⊢ ∀x. x ⊆ a → app f x = x → w ⊆ x, uq_intro 40.
42. 1, 2, 3 ⊢ w ⊆ a ∧ app f w = w, conj_intro 23 32.
43. 1, 2, 3 ⊢ w ⊆ a ∧ app f w = w ∧
  (∀x. x ⊆ a → app f x = x → w ⊆ x), conj_intro 42 41.
44. 1, 2, 3 ⊢ ∃u. u ⊆ a ∧ app f u = u ∧
  (∀x. x ⊆ a → app f x = x → u ⊆ x), ex_intro 43.
incl_Knaster_Tarski_least. ⊢ set a → map f (power a) (power a) →
  (∀x. ∀y. y ⊆ a → x ⊆ y → app f x ⊆ app f y) →
  ∃u. u ⊆ a ∧ app f u = u ∧ (∀x. x ⊆ a → app f x = x → u ⊆ x),
  subj_intro_iii 44.
end

begin
let M = {x | x ⊆ a ∧ x ⊆ app f x}.
let w = ⋃M.
01. 1 ⊢ set a, hypo.
02. 2 ⊢ map f (power a) (power a), hypo.
03. 3 ⊢ ∀x. ∀y. y ⊆ a → x ⊆ y → app f x ⊆ app f y, hypo.

04. 4 ⊢ x ∈ M, hypo.
05. 4 ⊢ x ⊆ a ∧ x ⊆ app f x, comp_elim 4.
06. 4 ⊢ x ⊆ a, conj_eliml 5.
07. ⊢ x ∈ M → x ⊆ a, subj_intro 6.
08. ⊢ ∀x. x ∈ M → x ⊆ a, uq_intro 7.
10. ⊢ w ⊆ a, Union_is_supremum 8.

11. 11 ⊢ x ∈ M, hypo.
12. ⊢ w = w, eq_refl.
13. 11 ⊢ x ⊆ w, Union_is_upper_bound 12 11.
14. 3 ⊢ ∀y. y ⊆ a → x ⊆ y → app f x ⊆ app f y, uq_elim 3.
15. 3 ⊢ x ⊆ w → app f x ⊆ app f w, uq_bounded_elim 14 10.
16. 3, 11 ⊢ app f x ⊆ app f w, subj_elim 15 13.
17. 11 ⊢ x ⊆ a ∧ x ⊆ app f x, comp_elim 11.
18. 11 ⊢ x ⊆ app f x, conj_elimr 17.
19. 3, 11 ⊢ x ⊆ app f w, incl_trans 18 16.
20. 3 ⊢ x ∈ M → x ⊆ app f w, subj_intro 19.
21. 3 ⊢ ∀x. x ∈ M → x ⊆ app f w, uq_intro 20.
22. 3 ⊢ w ⊆ app f w, Union_is_supremum 21.

24. 1 ⊢ set w, subset 10 1.
25. 1 ⊢ w ∈ power a, power_intro 24 10.
26. 1, 2 ⊢ app f w ∈ power a, map_app_in_cod 2 25.
27. 1, 2 ⊢ app f w ⊆ a, power_elim 26.
28. 3 ⊢ ∀y. y ⊆ a → w ⊆ y → app f w ⊆ app f y, uq_elim 3.
29. 1, 2, 3 ⊢ w ⊆ app f w → app f w ⊆ app f (app f w),
  uq_bounded_elim 28 27.
30. 1, 2, 3 ⊢ app f w ⊆ app f (app f w), subj_elim 29 22.
31. 1, 2, 3 ⊢ app f w ⊆ a ∧ app f w ⊆ app f (app f w),
  conj_intro 27 30.
32. 1, 2 ⊢ set (app f w), subset 27 1.
33. 1, 2, 3 ⊢ app f w ∈ M,
  comp_intro 32 31, P x ↔ x ⊆ a ∧ x ⊆ app f x.
34. 1, 2, 3 ⊢ app f w ⊆ w, Union_is_upper_bound 12 33.

35. 1, 2, 3 ⊢ app f w = w, incl_antisym 34 22.
36. 36 ⊢ x ⊆ a, hypo.
37. 37 ⊢ app f x = x, hypo.
38. 37 ⊢ x ⊆ app f x, incl_from_eq_rev 37.
39. 36, 37 ⊢ x ⊆ a ∧ x ⊆ app f x, conj_intro 36 38.
40. 1, 36 ⊢ set x, subset 36 1.
41. 1, 36, 37 ⊢ x ∈ M, comp_intro 40 39.
42. 1, 36, 37 ⊢ x ⊆ w, Union_is_upper_bound 12 41.
43. 1 ⊢ x ⊆ a → app f x = x → x ⊆ w, subj_intro_ii 42.
44. 1 ⊢ ∀x. x ⊆ a → app f x = x → x ⊆ w, uq_intro 43.
45. 1, 2, 3 ⊢ w ⊆ a ∧ app f w = w, conj_intro 10 35.
46. 1, 2, 3 ⊢ w ⊆ a ∧ app f w = w ∧
  (∀x. x ⊆ a → app f x = x → x ⊆ w), conj_intro 45 44.
47. 1, 2, 3 ⊢ ∃u. u ⊆ a ∧ app f u = u ∧
  (∀x. x ⊆ a → app f x = x → x ⊆ u), ex_intro 46.
incl_Knaster_Tarski_greatest. ⊢ set a →
  map f (power a) (power a) →
  (∀x. ∀y. y ⊆ a → x ⊆ y → app f x ⊆ app f y) →
  ∃u. u ⊆ a ∧ app f u = u ∧ (∀x. x ⊆ a → app f x = x → x ⊆ u),
  subj_intro_iii 47.
end

(*/note incl_Knaster_Tarski_least
The Knaster-Tarski fixed point theorem with respect to the the power
set lattice.
Let a be a set and let P(a) be its power set. Let f:\_P(a)\_→ P(a) be
a monotone function, i.e., for all x,\_y\_∈\_P(a), if x\_⊆\_y, then
f(x)\_⊆\_f(y). Then f has a least fixed point u, which is a set u\_⊆\_a
such that f(u)\_=\_u and for any other fixed point x (where x\_⊆\_a and
f(x)\_=\_x), it holds that u\_⊆\_x.<br>
<br>
<b>Proof idea</b><br>
The set M := {x ⊆ a | f(x) ⊆ x}, called the set of pre-fixed
points of\_f, serves as a set of candidates, because it is non-empty
since a\_∈\_M. The intersection ⋂M gives us a least candidate, which
turns out to be an exact fixed point. It then follows almost trivially
that this is also the least fixed point. In this respect, the proof
proceeds analogously to that of [[incl_Knaster_Tarski_greatest]].<br>
<br>
<b>Proof sketch</b><br>
Let M := {x ⊆ a | f(x) ⊆ x}, the set of all pre-fixed points of f
in P(a). Note that M is not empty, since a ∈ P(a) and the codomain
of f is P(a), which implies f(a) ⊆ a. Thus, a ∈ M. Now, let w := ⋂M,
the intersection of all sets in M. We will show that u := w is the
least fixed point of f. First, we show f(w) ⊆ w. Let x be an arbitrary
element of M. By the definition of intersection, w ⊆ x. Since f is
monotone, this implies f(w) ⊆ f(x). Furthermore, by the definition of
M, we have f(x) ⊆ x. By transitivity of inclusion, we get f(w) ⊆ x.
Since f(w) ⊆ x holds for all x ∈ M, f(w) is a lower bound for the
set M. Therefore, f(w) must be a subset of the greatest lower bound of
M, which is w. Thus, f(w) ⊆ w. Second, we show w ⊆ f(w). From the
previous step, we have f(w) ⊆ w. Applying the monotone function f to
both sides of this inclusion gives f(f(w)) ⊆ f(w). This result,
f(f(w)) ⊆ f(w), means that the set f(w) is itself an element of M.
Since f(w) ∈ M, and w is the intersection of all elements in M, w must
be a subset of f(w). Thus, w ⊆ f(w). Combining both inclusions,
f(w) ⊆ w and w ⊆ f(w), we conclude by the antisymmetry of set inclusion
that f(w) = w. It remains to show w that is the least fixed point.
Let x be any fixed point of f, meaning x ⊆ a and f(x) = x. From
f(x) = x, it follows that f(x) ⊆ x. This satisfies the condition for x
to be a member of M. Since x ∈ M, and w is the intersection of all sets
in M, it follows directly that w ⊆ x. q.e.d. *)

(*/note incl_Knaster_Tarski_greatest
The Knaster-Tarski fixed point theorem with respect to the the power
set lattice. Let a be a set, and let f: P(a) → P(a) be a monotone
function on its power set P(a) ordered by inclusion ⊆. Then f has a
greatest fixed point w.<br>
<br>
<b>Proof idea</b><br>
The set M := {x ⊆ a | x ⊆ f(x)}, called the set of post-fixed points of
f, serves as a set of candidates. The union ⋃M gives us a greatest
candidate, which turns out to be an exact fixed point. It then follows
almost trivially that this is also the greatest fixed point.
In this respect, the proof proceeds analogously to that of
[[incl_Knaster_Tarski_least]].<br>
<br>
<b>Proof sketch</b><br>
Let f: P(a) → P(a) be a monotone function, meaning that for any
x,\_y ∈ P(a), if x\_⊆\_y, then f(x) ⊆ f(y). Construct the candidate
fixed point. Let M := {x\_⊆\_a | x\_⊆\_f(x)}, the set of all
post-fixed points of\_f. Let w := ⋃M, the union of all sets in M.
We will show that u := w is the greatest fixed point of f.
First, we show w ⊆ f(w). Let x be an arbitrary element of M. By the
definition of M, we have x ⊆ f(x). By the definition of w as the union,
we also have x ⊆ w. Since f is monotone, x ⊆ w implies f(x) ⊆ f(w).
Combining these inclusions gives x ⊆ f(x) ⊆ f(w). Since this holds for
every x ∈ M, f(w) is an upper bound for M. As w is the least upper
bound (supremum) of M, it follows that w ⊆ f(w). Second, we show
f(w) ⊆ w. From the previous step, we have w ⊆ f(w). Applying the
monotone function f to both sides gives f(w) ⊆ f(f(w)). This inequality
f(w) ⊆ f(f(w)) means that f(w) itself is an element of M. Since w is
the union of all elements in M, w must contain f(w). Therefore,
f(w) ⊆ w. By the antisymmetry of inclusion, w ⊆ f(w) and f(w) ⊆ w
together imply that f(w) = w. Thus, w is a fixed point of f. It
remains to be shown that w is the greatest fixed point. Let x be any
fixed point of f, such that f(x) = x. This equality implies x ⊆ f(x),
which is the defining condition for x to be in M. Since x ∈ M, and w is
the union of all elements of M, it must be that x ⊆ w. Because any
fixed point x is a subset of w, w is the greatest fixed point.
q.e.d. *)
