
(*/chapter Relations *)

(*/section Relationhood *)

relation_unfold. ⊢ relation R → ∀t. t ∈ R → ∃x. ∃y. t = (x, y),
  bij_eliml relation_equi.

relation_fold. ⊢ (∀t. t ∈ R → ∃x. ∃y. t = (x, y)) → relation R,
  bij_elimr relation_equi.

01. 1 ⊢ relation R, hypo.
02. 2 ⊢ t ∈ R, hypo.
03. 1 ⊢ ∀t. t ∈ R → ∃x. ∃y. t = (x, y), relation_unfold 1.
04. 1, 2 ⊢ ∃x. ∃y. t = (x, y), uq_bounded_elim 3 2.
relation_elim. ⊢ relation R → t ∈ R → ∃x. ∃y. t = (x, y),
  subj_intro_ii 4.

01. 1 ⊢ R ⊆ X × Y, hypo.
02. 2 ⊢ t ∈ R, hypo.
03. 1, 2 ⊢ t ∈ X × Y, incl_elim 1 2.
04. 1, 2 ⊢ ∃x. ∃y. x ∈ X ∧ y ∈ Y ∧ t = (x, y), prod_elim 3.
05. 5 ⊢ ∃y. x ∈ X ∧ y ∈ Y ∧ t = (x, y), hypo.
06. 6 ⊢ x ∈ X ∧ y ∈ Y ∧ t = (x, y), hypo.
07. 6 ⊢ t = (x, y), conj_elimr 6.
08. 6 ⊢ ∃y. t = (x, y), ex_intro 7.
09. 6 ⊢ ∃x. ∃y. t = (x, y), ex_intro 8.
10. 5 ⊢ ∃x. ∃y. t = (x, y), ex_elim 5 9.
11. 1, 2 ⊢ ∃x. ∃y. t = (x, y), ex_elim 4 10.
12. 1 ⊢ t ∈ R → ∃x. ∃y. t = (x, y), subj_intro 11.
13. 1 ⊢ ∀t. t ∈ R → ∃x. ∃y. t = (x, y), uq_intro 12.
14. ⊢ (∀t. t ∈ R → ∃x. ∃y. t = (x, y)) → relation R,
  bij_elimr relation_equi.
15. 1 ⊢ relation R, subj_elim 14 13.
relation_from_incl. ⊢ R ⊆ X × Y → relation R, subj_intro 15.

01. 1 ⊢ R ⊆ Q, hypo.
02. 2 ⊢ relation Q, hypo.
03. 3 ⊢ t ∈ R, hypo.
04. 1, 3 ⊢ t ∈ Q, incl_elim 1 3.
05. 2 ⊢ ∀t. t ∈ Q → ∃x. ∃y. t = (x, y), relation_unfold 2.
06. 2 ⊢ t ∈ Q → ∃x. ∃y. t = (x, y), uq_elim 5.
07. 1, 2, 3 ⊢ ∃x. ∃y. t = (x, y), subj_elim 6 4.
08. 1, 2 ⊢ t ∈ R → ∃x. ∃y. t = (x, y), subj_intro 7.
09. 1, 2 ⊢ ∀t. t ∈ R → ∃x. ∃y. t = (x, y), uq_intro 8.
10. 1, 2 ⊢ relation R, relation_fold 9.
relation_subclass. ⊢ R ⊆ Q → relation Q → relation R, subj_intro_ii 10.

01. 1 ⊢ relation R, hypo.
02. 2 ⊢ relation Q, hypo.
03. 1 ⊢ ∀t. t ∈ R → ∃x. ∃y. t = (x, y), relation_unfold 1.
04. 2 ⊢ ∀t. t ∈ Q → ∃x. ∃y. t = (x, y), relation_unfold 2.
05. 1 ⊢ t ∈ R → ∃x. ∃y. t = (x, y), uq_elim 3.
06. 2 ⊢ t ∈ Q → ∃x. ∃y. t = (x, y), uq_elim 4.
07. 7 ⊢ t ∈ R ∪ Q, hypo.
08. 7 ⊢ t ∈ R ∨ t ∈ Q, union_elim 7.
09. 9 ⊢ t ∈ R, hypo.
10. 1, 9 ⊢ ∃x. ∃y. t = (x, y), subj_elim 5 9.
11. 11 ⊢ t ∈ Q, hypo.
12. 2, 11 ⊢ ∃x. ∃y. t = (x, y), subj_elim 6 11.
13. 1, 2, 7 ⊢ ∃x. ∃y. t = (x, y), disj_elim 8 10 12.
14. 1, 2 ⊢ t ∈ R ∪ Q → ∃x. ∃y. t = (x, y), subj_intro 13.
15. 1, 2 ⊢ ∀t. t ∈ R ∪ Q → ∃x. ∃y. t = (x, y), uq_intro 14.
16. 1, 2 ⊢ relation (R ∪ Q), relation_fold 15.
relation_union. ⊢ relation R → relation Q → relation (R ∪ Q),
  subj_intro_ii 16.


(*/section Domains and ranges *)

01. 1 ⊢ (x, y) ∈ R, hypo.
02. 1 ⊢ set (x, y), set_intro 1.
03. 1 ⊢ set x, setl_from_pair 2.
04. 1 ⊢ ∃y. (x, y) ∈ R, ex_intro 1.
05. 1 ⊢ x ∈ {x | ∃y. (x, y) ∈ R}, comp_intro 3 4.
06. 1 ⊢ x ∈ dom R, eq_subst_rev dom_eq 5, P u ↔ x ∈ u.
dom_intro. ⊢ (x, y) ∈ R → x ∈ dom R, subj_intro 6.

01. 1 ⊢ x ∈ dom R, hypo.
02. 1 ⊢ x ∈ {x | ∃y. (x, y) ∈ R}, eq_subst dom_eq 1, P u ↔ x ∈ u.
03. 1 ⊢ ∃y. (x, y) ∈ R, comp_elim 2.
dom_elim. ⊢ x ∈ dom R → ∃y. (x, y) ∈ R, subj_intro 3.

01. 1 ⊢ R ⊆ X × Y, hypo.
02. 2 ⊢ x ∈ dom R, hypo.
03. 2 ⊢ ∃y. (x, y) ∈ R, dom_elim 2.
04. 4 ⊢ (x, y) ∈ R, hypo.
05. 1, 4 ⊢ (x, y) ∈ X × Y, incl_elim 1 4.
06. 1, 4 ⊢ x ∈ X ∧ y ∈ Y, prod_elim_pair 5.
07. 1, 4 ⊢ x ∈ X, conj_eliml 6.
08. 1, 2 ⊢ x ∈ X, ex_elim 3 7.
09. 1 ⊢ x ∈ dom R → x ∈ X, subj_intro 8.
10. 1 ⊢ ∀x. x ∈ dom R → x ∈ X, uq_intro 9.
11. 1 ⊢ dom R ⊆ X, incl_intro 10.
dom_incl. ⊢ R ⊆ X × Y → dom R ⊆ X, subj_intro 11.

01. 1 ⊢ (x, y) ∈ R, hypo.
02. 1 ⊢ set (x, y), set_intro 1.
03. 1 ⊢ set y, setr_from_pair 2.
04. 1 ⊢ ∃x. (x, y) ∈ R, ex_intro 1.
05. 1 ⊢ y ∈ {y | ∃x. (x, y) ∈ R}, comp_intro 3 4.
06. 1 ⊢ y ∈ rng R, eq_subst_rev rng_eq 5, P u ↔ y ∈ u.
rng_intro. ⊢ (x, y) ∈ R → y ∈ rng R, subj_intro 6.

01. 1 ⊢ y ∈ rng R, hypo.
02. 1 ⊢ y ∈ {y | ∃x. (x, y) ∈ R}, eq_subst rng_eq 1, P u ↔ y ∈ u.
03. 1 ⊢ ∃x. (x, y) ∈ R, comp_elim 2.
rng_elim. ⊢ y ∈ rng R → ∃x. (x, y) ∈ R, subj_intro 3.

01. 1 ⊢ R ⊆ Q, hypo.
02. 2 ⊢ y ∈ rng R, hypo.
03. 2 ⊢ ∃x. (x, y) ∈ R, rng_elim 2.
04. 4 ⊢ (x, y) ∈ R, hypo.
05. 1, 4 ⊢ (x, y) ∈ Q, incl_elim 1 4.
06. 1, 4 ⊢ y ∈ rng Q, rng_intro 5.
07. 1, 2 ⊢ y ∈ rng Q, ex_elim 3 6.
08. 1 ⊢ y ∈ rng R → y ∈ rng Q, subj_intro 7.
09. 1 ⊢ ∀y. y ∈ rng R → y ∈ rng Q, uq_intro 8.
10. 1 ⊢ rng R ⊆ rng Q, incl_intro 9.
rng_subclass. ⊢ R ⊆ Q → rng R ⊆ rng Q, subj_intro 10.

01. 1 ⊢ R ⊆ X × Y, hypo.
02. 2 ⊢ x ∈ dom R, hypo.
03. 2 ⊢ ∃y. (x, y) ∈ R, dom_elim 2.
04. 4 ⊢ (x, y) ∈ R, hypo.
05. 1, 4 ⊢ (x, y) ∈ X × Y, incl_elim 1 4.
06. 1, 4 ⊢ x ∈ X ∧ y ∈ Y, prod_elim_pair 5.
07. 1, 4 ⊢ x ∈ X, conj_eliml 6.
08. 1, 2 ⊢ x ∈ X, ex_elim 3 7.
09. 1 ⊢ x ∈ dom R → x ∈ X, subj_intro 8.
10. 1 ⊢ ∀x. x ∈ dom R → x ∈ X, uq_intro 9.
11. 1 ⊢ dom R ⊆ X, incl_intro 10.
dom_of_subclass_prod. ⊢ R ⊆ X × Y → dom R ⊆ X, subj_intro 11.

01. 1 ⊢ R ⊆ X × Y, hypo.
02. 2 ⊢ y ∈ rng R, hypo.
03. 2 ⊢ ∃x. (x, y) ∈ R, rng_elim 2.
04. 4 ⊢ (x, y) ∈ R, hypo.
05. 1, 4 ⊢ (x, y) ∈ X × Y, incl_elim 1 4.
06. 1, 4 ⊢ x ∈ X ∧ y ∈ Y, prod_elim_pair 5.
07. 1, 4 ⊢ y ∈ Y, conj_elimr 6.
08. 1, 2 ⊢ y ∈ Y, ex_elim 3 7.
09. 1 ⊢ y ∈ rng R → y ∈ Y, subj_intro 8.
10. 1 ⊢ ∀y. y ∈ rng R → y ∈ Y, uq_intro 9.
11. 1 ⊢ rng R ⊆ Y, incl_intro 10.
rng_of_subclass_prod. ⊢ R ⊆ X × Y → rng R ⊆ Y, subj_intro 11.

1. 1 ⊢ relation R, hypo.
2. 2 ⊢ dom R ⊆ X, hypo.
3. 3 ⊢ rng R ⊆ Y, hypo.
4. 4 ⊢ t ∈ R, hypo.
5. 1 ⊢ ∀t. t ∈ R → ∃x. ∃y. t = (x, y), relation_unfold 1.
6. 1 ⊢ t ∈ R → ∃x. ∃y. t = (x, y), uq_elim 5.
7. 1, 4 ⊢ ∃x. ∃y. t = (x, y), subj_elim 6 4.
8. 8 ⊢ ∃y. t = (x, y), hypo.
9. 9 ⊢ t = (x, y), hypo.
10. 4, 9 ⊢ (x, y) ∈ R, eq_subst 9 4, P u ↔ u ∈ R.
11. 4, 9 ⊢ x ∈ dom R, dom_intro 10.
12. 2, 4, 9 ⊢ x ∈ X, incl_elim 2 11.
13. 4, 9 ⊢ y ∈ rng R, rng_intro 10.
14. 3, 4, 9 ⊢ y ∈ Y, incl_elim 3 13.
15. 2, 3, 4, 9 ⊢ (x, y) ∈ X × Y, prod_intro 12 14.
16. 2, 3, 4, 9 ⊢ t ∈ X × Y, eq_subst_rev 9 15, P u ↔ u ∈ X × Y.
17. 2, 3, 4, 8 ⊢ t ∈ X × Y, ex_elim 8 16.
18. 1, 2, 3, 4 ⊢ t ∈ X × Y, ex_elim 7 17.
19. 1, 2, 3 ⊢ t ∈ R → t ∈ X × Y, subj_intro 18.
20. 1, 2, 3 ⊢ ∀t. t ∈ R → t ∈ X × Y, uq_intro 19.
21. 1, 2, 3 ⊢ R ⊆ X × Y, incl_intro 20.
relation_incl_prod. ⊢ relation R →
  dom R ⊆ X → rng R ⊆ Y → R ⊆ X × Y, subj_intro_iii 21.

01. 1 ⊢ x ∈ dom (R ∪ Q), hypo.
02. 1 ⊢ ∃y. (x, y) ∈ R ∪ Q, dom_elim 1.
03. 3 ⊢ (x, y) ∈ R ∪ Q, hypo.
04. 3 ⊢ (x, y) ∈ R ∨ (x, y) ∈ Q, union_elim 3.
05. 5 ⊢ (x, y) ∈ R, hypo.
06. 5 ⊢ x ∈ dom R, dom_intro 5.
07. 5 ⊢ x ∈ dom R ∪ dom Q, union_introl 6.
08. 8 ⊢ (x, y) ∈ Q, hypo.
09. 8 ⊢ x ∈ dom Q, dom_intro 8.
10. 8 ⊢ x ∈ dom R ∪ dom Q, union_intror 9.
11. 3 ⊢ x ∈ dom R ∪ dom Q, disj_elim 4 7 10.
12. 1 ⊢ x ∈ dom R ∪ dom Q, ex_elim 2 11.
13. ⊢ x ∈ dom (R ∪ Q) → x ∈ dom R ∪ dom Q, subj_intro 12.
14. 14 ⊢ x ∈ dom R ∪ dom Q, hypo.
15. 14 ⊢ x ∈ dom R ∨ x ∈ dom Q, union_elim 14.
16. 16 ⊢ x ∈ dom R, hypo.
17. 16 ⊢ ∃y. (x, y) ∈ R, dom_elim 16.
18. 18 ⊢ (x, y) ∈ R, hypo.
19. 18 ⊢ (x, y) ∈ R ∪ Q, union_introl 18.
20. 18 ⊢ x ∈ dom (R ∪ Q), dom_intro 19.
21. 16 ⊢ x ∈ dom (R ∪ Q), ex_elim 17 20.
22. 22 ⊢ x ∈ dom Q, hypo.
23. 22 ⊢ ∃y. (x, y) ∈ Q, dom_elim 22.
24. 24 ⊢ (x, y) ∈ Q, hypo.
25. 24 ⊢ (x, y) ∈ R ∪ Q, union_intror 24.
26. 24 ⊢ x ∈ dom (R ∪ Q), dom_intro 25.
27. 22 ⊢ x ∈ dom (R ∪ Q), ex_elim 23 26.
28. 14 ⊢ x ∈ dom (R ∪ Q), disj_elim 15 21 27.
29. ⊢ x ∈ dom R ∪ dom Q → x ∈ dom (R ∪ Q), subj_intro 28.
30. ⊢ x ∈ dom (R ∪ Q) ↔ x ∈ dom R ∪ dom Q, bij_intro 13 29.
31. ⊢ ∀x. x ∈ dom (R ∪ Q) ↔ x ∈ dom R ∪ dom Q, uq_intro 30.
dom_union. ⊢ dom (R ∪ Q) = dom R ∪ dom Q, ext 31.

01. 1 ⊢ y ∈ rng (R ∪ Q), hypo.
02. 1 ⊢ ∃x. (x, y) ∈ R ∪ Q, rng_elim 1.
03. 3 ⊢ (x, y) ∈ R ∪ Q, hypo.
04. 3 ⊢ (x, y) ∈ R ∨ (x, y) ∈ Q, union_elim 3.
05. 5 ⊢ (x, y) ∈ R, hypo.
06. 5 ⊢ y ∈ rng R, rng_intro 5.
07. 5 ⊢ y ∈ rng R ∪ rng Q, union_introl 6.
08. 8 ⊢ (x, y) ∈ Q, hypo.
09. 8 ⊢ y ∈ rng Q, rng_intro 8.
10. 8 ⊢ y ∈ rng R ∪ rng Q, union_intror 9.
11. 3 ⊢ y ∈ rng R ∪ rng Q, disj_elim 4 7 10.
12. 1 ⊢ y ∈ rng R ∪ rng Q, ex_elim 2 11.
13. ⊢ y ∈ rng (R ∪ Q) → y ∈ rng R ∪ rng Q, subj_intro 12.
14. 14 ⊢ y ∈ rng R ∪ rng Q, hypo.
15. 14 ⊢ y ∈ rng R ∨ y ∈ rng Q, union_elim 14.
16. 16 ⊢ y ∈ rng R, hypo.
17. 16 ⊢ ∃x. (x, y) ∈ R, rng_elim 16.
18. 18 ⊢ (x, y) ∈ R, hypo.
19. 18 ⊢ (x, y) ∈ R ∪ Q, union_introl 18.
20. 18 ⊢ y ∈ rng (R ∪ Q), rng_intro 19.
21. 16 ⊢ y ∈ rng (R ∪ Q), ex_elim 17 20.
22. 22 ⊢ y ∈ rng Q, hypo.
23. 22 ⊢ ∃x. (x, y) ∈ Q, rng_elim 22.
24. 24 ⊢ (x, y) ∈ Q, hypo.
25. 24 ⊢ (x, y) ∈ R ∪ Q, union_intror 24.
26. 24 ⊢ y ∈ rng (R ∪ Q), rng_intro 25.
27. 22 ⊢ y ∈ rng (R ∪ Q), ex_elim 23 26.
28. 14 ⊢ y ∈ rng (R ∪ Q), disj_elim 15 21 27.
29. ⊢ y ∈ rng R ∪ rng Q → y ∈ rng (R ∪ Q), subj_intro 28.
30. ⊢ y ∈ rng (R ∪ Q) ↔ y ∈ rng R ∪ rng Q, bij_intro 13 29.
31. ⊢ ∀y. y ∈ rng (R ∪ Q) ↔ y ∈ rng R ∪ rng Q, uq_intro 30.
rng_union. ⊢ rng (R ∪ Q) = rng R ∪ rng Q, ext 31.


(*/section Images and inverse images *)

01. 1 ⊢ x ∈ A, hypo.
02. 2 ⊢ (x, y) ∈ R, hypo.
03. 2 ⊢ set (x, y), set_intro 2.
04. 2 ⊢ set y, setr_from_pair 3.
05. 1, 2 ⊢ x ∈ A ∧ (x, y) ∈ R, conj_intro 1 2.
06. 1, 2 ⊢ ∃x. x ∈ A ∧ (x, y) ∈ R, ex_intro 5.
07. 1, 2 ⊢ y ∈ {y | ∃x. x ∈ A ∧ (x, y) ∈ R}, comp_intro 4 6.
08. 1, 2 ⊢ y ∈ img R A, eq_subst_rev img_eq 7, P u ↔ y ∈ u.
img_intro. ⊢ x ∈ A → (x, y) ∈ R → y ∈ img R A, subj_intro_ii 8.

01. 1 ⊢ y ∈ img R A, hypo.
02. 1 ⊢ y ∈ {y | ∃x. x ∈ A ∧ (x, y) ∈ R},
  eq_subst img_eq 1, P u ↔ y ∈ u.
03. 1 ⊢ ∃x. x ∈ A ∧ (x, y) ∈ R, comp_elim 2.
img_elim. ⊢ y ∈ img R A → ∃x. x ∈ A ∧ (x, y) ∈ R, subj_intro 3.

01. 1 ⊢ y ∈ B, hypo.
02. 2 ⊢ (x, y) ∈ R, hypo.
03. 2 ⊢ set (x, y), set_intro 2.
04. 2 ⊢ set x, setl_from_pair 3.
05. 1, 2 ⊢ y ∈ B ∧ (x, y) ∈ R, conj_intro 1 2.
06. 1, 2 ⊢ ∃y. y ∈ B ∧ (x, y) ∈ R, ex_intro 5.
07. 1, 2 ⊢ x ∈ {x | ∃y. y ∈ B ∧ (x, y) ∈ R}, comp_intro 4 6.
08. 1, 2 ⊢ x ∈ inv_img R B, eq_subst_rev inv_img_eq 7, P u ↔ x ∈ u.
inv_img_intro. ⊢ y ∈ B → (x, y) ∈ R → x ∈ inv_img R B, subj_intro_ii 8.

01. 1 ⊢ x ∈ inv_img R B, hypo.
02. 1 ⊢ x ∈ {x | ∃y. y ∈ B ∧ (x, y) ∈ R},
  eq_subst inv_img_eq 1, P u ↔ x ∈ u.
03. 1 ⊢ ∃y. y ∈ B ∧ (x, y) ∈ R, comp_elim 2.
inv_img_elim. ⊢ x ∈ inv_img R B → ∃y. y ∈ B ∧ (x, y) ∈ R, subj_intro 3.

01. 1 ⊢ y ∈ img R (A ∪ B), hypo.
02. 1 ⊢ ∃x. x ∈ A ∪ B ∧ (x, y) ∈ R, img_elim 1.
03. 3 ⊢ x ∈ A ∪ B ∧ (x, y) ∈ R, hypo.
04. 3 ⊢ (x, y) ∈ R, conj_elimr 3.
05. 3 ⊢ x ∈ A ∪ B, conj_eliml 3.
06. 3 ⊢ x ∈ A ∨ x ∈ B, union_elim 5.
07. 7 ⊢ x ∈ A, hypo.
08. 3, 7 ⊢ y ∈ img R A, img_intro 7 4.
09. 3, 7 ⊢ y ∈ img R A ∪ img R B, union_introl 8.
10. 10 ⊢ x ∈ B, hypo.
11. 3, 10 ⊢ y ∈ img R B, img_intro 10 4.
12. 3, 10 ⊢ y ∈ img R A ∪ img R B, union_intror 11.
13. 3 ⊢ y ∈ img R A ∪ img R B, disj_elim 6 9 12.
14. 1 ⊢ y ∈ img R A ∪ img R B, ex_elim 2 13.
15. ⊢ y ∈ img R (A ∪ B) → y ∈ img R A ∪ img R B, subj_intro 14.

16. 16 ⊢ y ∈ img R A ∪ img R B, hypo.
17. 16 ⊢ y ∈ img R A ∨ y ∈ img R B, union_elim 16.
18. 18 ⊢ y ∈ img R A, hypo.
19. 18 ⊢ ∃x. x ∈ A ∧ (x, y) ∈ R, img_elim 18.
20. 20 ⊢ x ∈ A ∧ (x, y) ∈ R, hypo.
21. 20 ⊢ x ∈ A, conj_eliml 20.
22. 20 ⊢ (x, y) ∈ R, conj_elimr 20.
23. 20 ⊢ x ∈ A ∪ B, union_introl 21.
24. 20 ⊢ y ∈ img R (A ∪ B), img_intro 23 22.
25. 18 ⊢ y ∈ img R (A ∪ B), ex_elim 19 24.
26. 26 ⊢ y ∈ img R B, hypo.
27. 26 ⊢ ∃x. x ∈ B ∧ (x, y) ∈ R, img_elim 26.
28. 28 ⊢ x ∈ B ∧ (x, y) ∈ R, hypo.
29. 28 ⊢ x ∈ B, conj_eliml 28.
30. 28 ⊢ (x, y) ∈ R, conj_elimr 28.
31. 28 ⊢ x ∈ A ∪ B, union_intror 29.
32. 28 ⊢ y ∈ img R (A ∪ B), img_intro 31 30.
33. 26 ⊢ y ∈ img R (A ∪ B), ex_elim 27 32.
34. 16 ⊢ y ∈ img R (A ∪ B), disj_elim 17 25 33.
35. ⊢ y ∈ img R A ∪ img R B → y ∈ img R (A ∪ B), subj_intro 34.

36. ⊢ y ∈ img R (A ∪ B) ↔ y ∈ img R A ∪ img R B, bij_intro 15 35.
37. ⊢ ∀y. y ∈ img R (A ∪ B) ↔ y ∈ img R A ∪ img R B, uq_intro 36.
img_dist_union. ⊢ img R (A ∪ B) = img R A ∪ img R B, ext 37.

01. 1 ⊢ y ∈ img R (A ∩ B), hypo.
02. 1 ⊢ ∃x. x ∈ A ∩ B ∧ (x, y) ∈ R, img_elim 1.
03. 3 ⊢ x ∈ A ∩ B ∧ (x, y) ∈ R, hypo.
04. 3 ⊢ x ∈ A ∩ B, conj_eliml 3.
05. 3 ⊢ x ∈ A, intersection_eliml 4.
06. 3 ⊢ x ∈ B, intersection_elimr 4.
07. 3 ⊢ (x, y) ∈ R, conj_elimr 3.
08. 3 ⊢ y ∈ img R A, img_intro 5 7.
09. 3 ⊢ y ∈ img R B, img_intro 6 7.
10. 3 ⊢ y ∈ img R A ∩ img R B, intersection_intro 8 9.
11. 1 ⊢ y ∈ img R A ∩ img R B, ex_elim 2 10.
12. ⊢ y ∈ img R (A ∩ B) → y ∈ img R A ∩ img R B, subj_intro 11.
13. ⊢ ∀y. y ∈ img R (A ∩ B) → y ∈ img R A ∩ img R B, uq_intro 12.
img_dist_inter. ⊢ img R (A ∩ B) ⊆ img R A ∩ img R B, incl_intro 13.

01. 1 ⊢ A ⊆ B, hypo.
02. 1 ⊢ A ∪ B = B, union_from_incl 1.
03. 1 ⊢ img R (A ∪ B) = img R B, eq_cong 2, f u = img R u.
04. ⊢ img R A ∪ img R B = img R (A ∪ B), eq_symm img_dist_union.
05. 1 ⊢ img R A ∪ img R B = img R B, eq_trans 4 3.
06. 1 ⊢ img R A ⊆ img R B, incl_from_union 5.
img_incl. ⊢ A ⊆ B → img R A ⊆ img R B, subj_intro 6.

01. 1 ⊢ y ∈ img R A, hypo.
02. 1 ⊢ ∃x. x ∈ A ∧ (x, y) ∈ R, img_elim 1.
03. 3 ⊢ x ∈ A ∧ (x, y) ∈ R, hypo.
04. 3 ⊢ (x, y) ∈ R, conj_elimr 3.
05. 3 ⊢ y ∈ rng R, rng_intro 4.
06. 1 ⊢ y ∈ rng R, ex_elim 2 5.
07. ⊢ y ∈ img R A → y ∈ rng R, subj_intro 6.
08. ⊢ ∀y. y ∈ img R A → y ∈ rng R, uq_intro 7.
img_incl_in_rng. ⊢ img R A ⊆ rng R, incl_intro 8.

01. 1 ⊢ relation R, hypo.
02. 2 ⊢ y ∈ rng R, hypo.
03. 2 ⊢ ∃x. (x, y) ∈ R, rng_elim 2.
04. 4 ⊢ (x, y) ∈ R, hypo.
05. 4 ⊢ x ∈ dom R, dom_intro 4.
06. 4 ⊢ y ∈ img R (dom R), img_intro 5 4.
07. 2 ⊢ y ∈ img R (dom R), ex_elim 3 6.
08. ⊢ y ∈ rng R → y ∈ img R (dom R), subj_intro 7.
09. ⊢ ∀y. y ∈ rng R → y ∈ img R (dom R), uq_intro 8.
10. ⊢ rng R ⊆ img R (dom R), incl_intro 9.
11. ⊢ img R (dom R) ⊆ rng R, img_incl_in_rng.
img_of_dom_is_rng. ⊢ img R (dom R) = rng R, incl_antisym 11 10.

01. 1 ⊢ x ∈ inv_img R (A ∩ B), hypo.
02. 1 ⊢ ∃y. y ∈ A ∩ B ∧ (x, y) ∈ R, inv_img_elim 1.
03. 3 ⊢ y ∈ A ∩ B ∧ (x, y) ∈ R, hypo.
04. 3 ⊢ y ∈ A ∩ B, conj_eliml 3.
05. 3 ⊢ y ∈ A, intersection_eliml 4.
06. 3 ⊢ y ∈ B, intersection_elimr 4.
07. 3 ⊢ (x, y) ∈ R, conj_elimr 3.
08. 3 ⊢ x ∈ inv_img R A, inv_img_intro 5 7.
09. 3 ⊢ x ∈ inv_img R B, inv_img_intro 6 7.
10. 3 ⊢ x ∈ inv_img R A ∩ inv_img R B, intersection_intro 8 9.
11. 1 ⊢ x ∈ inv_img R A ∩ inv_img R B, ex_elim 2 10.
12. ⊢ x ∈ inv_img R (A ∩ B) → x ∈ inv_img R A ∩ inv_img R B,
  subj_intro 11.
13. ⊢ ∀x. x ∈ inv_img R (A ∩ B) → x ∈ inv_img R A ∩ inv_img R B,
  uq_intro 12.
inv_img_dist_inter. ⊢ inv_img R (A ∩ B) ⊆ inv_img R A ∩ inv_img R B,
  incl_intro 13.

01. 1 ⊢ x ∈ inv_img R (A ∪ B), hypo.
02. 1 ⊢ ∃y. y ∈ A ∪ B ∧ (x, y) ∈ R, inv_img_elim 1.
03. 3 ⊢ y ∈ A ∪ B ∧ (x, y) ∈ R, hypo.
04. 3 ⊢ (x, y) ∈ R, conj_elimr 3.
05. 3 ⊢ y ∈ A ∪ B, conj_eliml 3.
06. 3 ⊢ y ∈ A ∨ y ∈ B, union_elim 5.
07. 7 ⊢ y ∈ A, hypo.
08. 3, 7 ⊢ x ∈ inv_img R A, inv_img_intro 7 4.
09. 3, 7 ⊢ x ∈ inv_img R A ∪ inv_img R B, union_introl 8.
10. 10 ⊢ y ∈ B, hypo.
11. 3, 10 ⊢ x ∈ inv_img R B, inv_img_intro 10 4.
12. 3, 10 ⊢ x ∈ inv_img R A ∪ inv_img R B, union_intror 11.
13. 3 ⊢ x ∈ inv_img R A ∪ inv_img R B, disj_elim 6 9 12.
14. 1 ⊢ x ∈ inv_img R A ∪ inv_img R B, ex_elim 2 13.
15. ⊢ x ∈ inv_img R (A ∪ B) → x ∈ inv_img R A ∪ inv_img R B,
  subj_intro 14.

16. 16 ⊢ x ∈ inv_img R A ∪ inv_img R B, hypo.
17. 16 ⊢ x ∈ inv_img R A ∨ x ∈ inv_img R B, union_elim 16.
18. 18 ⊢ x ∈ inv_img R A, hypo.
19. 18 ⊢ ∃y. y ∈ A ∧ (x, y) ∈ R, inv_img_elim 18.
20. 20 ⊢ y ∈ A ∧ (x, y) ∈ R, hypo.
21. 20 ⊢ y ∈ A, conj_eliml 20.
22. 20 ⊢ y ∈ A ∪ B, union_introl 21.
23. 20 ⊢ (x, y) ∈ R, conj_elimr 20.
24. 20 ⊢ x ∈ inv_img R (A ∪ B), inv_img_intro 22 23.
25. 18 ⊢ x ∈ inv_img R (A ∪ B), ex_elim 19 24.
26. 26 ⊢ x ∈ inv_img R B, hypo.
27. 26 ⊢ ∃y. y ∈ B ∧ (x, y) ∈ R, inv_img_elim 26.
28. 28 ⊢ y ∈ B ∧ (x, y) ∈ R, hypo.
29. 28 ⊢ y ∈ B, conj_eliml 28.
30. 28 ⊢ y ∈ A ∪ B, union_intror 29.
31. 28 ⊢ (x, y) ∈ R, conj_elimr 28.
32. 28 ⊢ x ∈ inv_img R (A ∪ B), inv_img_intro 30 31.
33. 26 ⊢ x ∈ inv_img R (A ∪ B), ex_elim 27 32.
34. 16 ⊢ x ∈ inv_img R (A ∪ B), disj_elim 17 25 33.
35. ⊢ x ∈ inv_img R A ∪ inv_img R B → x ∈ inv_img R (A ∪ B),
  subj_intro 34.

36. ⊢ x ∈ inv_img R (A ∪ B) ↔ x ∈ inv_img R A ∪ inv_img R B,
  bij_intro 15 35.
37. ⊢ ∀x. x ∈ inv_img R (A ∪ B) ↔ x ∈ inv_img R A ∪ inv_img R B,
  uq_intro 36.
inv_img_dist_union. ⊢ inv_img R (A ∪ B) = inv_img R A ∪ inv_img R B,
  ext 37.

01. 1 ⊢ A ⊆ B, hypo.
02. 1 ⊢ A ∪ B = B, union_from_incl 1.
03. 1 ⊢ inv_img R (A ∪ B) = inv_img R B, eq_cong 2, f u = inv_img R u.
04. ⊢ inv_img R A ∪ inv_img R B = inv_img R (A ∪ B),
  eq_symm inv_img_dist_union.
05. 1 ⊢ inv_img R A ∪ inv_img R B = inv_img R B, eq_trans 4 3.
06. 1 ⊢ inv_img R A ⊆ inv_img R B, incl_from_union 5.
inv_img_incl. ⊢ A ⊆ B → inv_img R A ⊆ inv_img R B, subj_intro 6.

01. 1 ⊢ x ∈ inv_img R B, hypo.
02. 1 ⊢ ∃y. y ∈ B ∧ (x, y) ∈ R, inv_img_elim 1.
03. 3 ⊢ y ∈ B ∧ (x, y) ∈ R, hypo.
04. 3 ⊢ (x, y) ∈ R, conj_elimr 3.
05. 3 ⊢ x ∈ dom R, dom_intro 4.
06. 1 ⊢ x ∈ dom R, ex_elim 2 5.
07. ⊢ x ∈ inv_img R B → x ∈ dom R, subj_intro 6.
08. ⊢ ∀x. x ∈ inv_img R B → x ∈ dom R, uq_intro 7.
inv_img_incl_in_dom. ⊢ inv_img R B ⊆ dom R, incl_intro 8.

01. 1 ⊢ y ∈ img (R ∪ Q) A, hypo.
02. 1 ⊢ ∃x. x in A and (x, y) ∈ R ∪ Q, img_elim 1.
03. 3 ⊢ x in A and (x, y) ∈ R ∪ Q, hypo.
04. 3 |- x in A, conj_eliml 3.
05. 3 |- (x, y) in R cup Q, conj_elimr 3.
06. 3 ⊢ (x, y) ∈ R ∨ (x, y) ∈ Q, union_elim 5.
07. 7 ⊢ (x, y) ∈ R, hypo.
08. 3, 7 ⊢ y ∈ img R A, img_intro 4 7.
09. 3, 7 ⊢ y ∈ img R A ∪ img Q A, union_introl 8.
10. 10 ⊢ (x, y) ∈ Q, hypo.
11. 3, 10 ⊢ y ∈ img Q A, img_intro 4 10.
12. 3, 10 ⊢ y ∈ img R A ∪ img Q A, union_intror 11.
13. 3 ⊢ y ∈ img R A ∪ img Q A, disj_elim 6 9 12.
14. 1 ⊢ y ∈ img R A ∪ img Q A, ex_elim 2 13.
15. ⊢ y ∈ img (R ∪ Q) A → y ∈ img R A ∪ img Q A, subj_intro 14.
16. 16 ⊢ y ∈ img R A ∪ img Q A, hypo.
17. 16 ⊢ y ∈ img R A ∨ y ∈ img Q A, union_elim 16.
18. 18 ⊢ y ∈ img R A, hypo.
19. 18 ⊢ ∃x. x in A and (x, y) ∈ R, img_elim 18.
20. 20 ⊢ x in A and (x, y) ∈ R, hypo.
21. 20 |- x in A, conj_eliml 20.
22. 20 |- (x, y) in R, conj_elimr 20.
23. 20 ⊢ (x, y) ∈ R ∪ Q, union_introl 22.
24. 20 ⊢ y ∈ img (R ∪ Q) A, img_intro 21 23.
25. 18 ⊢ y ∈ img (R ∪ Q) A, ex_elim 19 24.
26. 26 ⊢ y ∈ img Q A, hypo.
27. 26 ⊢ ∃x. x in A and (x, y) ∈ Q, img_elim 26.
28. 28 ⊢ x in A and (x, y) ∈ Q, hypo.
29. 28 |- x in A, conj_eliml 28.
30. 28 |- (x, y) in Q, conj_elimr 28.
31. 28 ⊢ (x, y) ∈ R ∪ Q, union_intror 30.
32. 28 ⊢ y ∈ img (R ∪ Q) A, img_intro 29 31.
33. 26 ⊢ y ∈ img (R ∪ Q) A, ex_elim 27 32.
34. 16 ⊢ y ∈ img (R ∪ Q) A, disj_elim 17 25 33.
35. ⊢ y ∈ img R A ∪ img Q A → y ∈ img (R ∪ Q) A, subj_intro 34.
36. ⊢ y ∈ img (R ∪ Q) A ↔ y ∈ img R A ∪ img Q A, bij_intro 15 35.
37. ⊢ ∀y. y ∈ img (R ∪ Q) A ↔ y ∈ img R A ∪ img Q A, uq_intro 36.
img_union. ⊢ img (R ∪ Q) A = img R A ∪ img Q A, ext 37.


(*/section Composition of relations *)

01. 1 ⊢ t ∈ S ∘ R, hypo.
02. ⊢ S ∘ R = {t | ∃x. ∃y. ∃z. t = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S},
  composition_eq.
03. 1 ⊢ t ∈ {t | ∃x. ∃y. ∃z. t = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S},
  eq_subst 2 1, P u ↔ t ∈ u.
04. 1 ⊢ ∃x. ∃y. ∃z. t = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S,
  comp_elim 3.
05. 5 ⊢ ∃y. ∃z. t = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S, hypo.
06. 6 ⊢ ∃z. t = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S, hypo.
07. 7 ⊢ t = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S, hypo.
08. 7 ⊢ t = (x, z), conj_elimll 7.
09. 7 ⊢ ∃z. t = (x, z), ex_intro 8.
10. 7 ⊢ ∃x. ∃z. t = (x, z), ex_intro 9.
11. 6 ⊢ ∃x. ∃z. t = (x, z), ex_elim 6 10.
12. 5 ⊢ ∃x. ∃z. t = (x, z), ex_elim 5 11.
13. 1 ⊢ ∃x. ∃z. t = (x, z), ex_elim 4 12.
14. ⊢ t ∈ S ∘ R → ∃x. ∃z. t = (x, z), subj_intro 13.
15. ⊢ ∀t. t ∈ S ∘ R → ∃x. ∃z. t = (x, z), uq_intro 14.
composition_is_relation. ⊢ relation (S ∘ R), relation_fold 15.

01. 1 ⊢ (a, c) ∈ S ∘ R, hypo.
02. ⊢ S ∘ R = {t | ∃x. ∃y. ∃z. t = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S},
  composition_eq.
03. 1 ⊢ (a, c) ∈ {t | ∃x. ∃y. ∃z. t = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S},
  eq_subst 2 1, P u ↔ (a, c) ∈ u.
04. 1 ⊢ ∃x. ∃y. ∃z. (a, c) = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S,
  comp_elim 3.
05. 5 ⊢ ∃y. ∃z. (a, c) = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S, hypo.
06. 6 ⊢ ∃z. (a, c) = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S, hypo.
07. 7 ⊢ (a, c) = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S, hypo.
08. 7 ⊢ (a, c) = (x, z), conj_elimll 7.
09. 1 ⊢ set (a, c), set_intro 1.
10. 1 ⊢ set a, setl_from_pair 9.
11. 1 ⊢ set c, setr_from_pair 9.
12. 1, 7 ⊢ a = x ∧ c = z, pair_property 10 11 8.
13. 1, 7 ⊢ a = x, conj_eliml 12.
14. 1, 7 ⊢ c = z, conj_elimr 12.
15. 7 ⊢ (x, y) ∈ R, conj_elimlr 7.
16. 1, 7 ⊢ (a, y) ∈ R, eq_subst_rev 13 15.
17. 1, 7 ⊢ ∃y. (a, y) ∈ R, ex_intro 16.
18. 1, 6 ⊢ ∃y. (a, y) ∈ R, ex_elim 6 17.
19. 1, 5 ⊢ ∃y. (a, y) ∈ R, ex_elim 5 18.
20. 1 ⊢ ∃y. (a, y) ∈ R, ex_elim 4 19.
composition_elimr. ⊢ (a, c) ∈ S ∘ R → ∃y. (a, y) ∈ R, subj_intro 20.

21. 7 ⊢ (y, z) ∈ S, conj_elimr 7.
22. 1, 7 ⊢ (y, c) ∈ S, eq_subst_rev 14 21.
23. 1, 7 ⊢ ∃y. (y, c) ∈ S, ex_intro 22.
24. 1, 6 ⊢ ∃y. (y, c) ∈ S, ex_elim 6 23.
25. 1, 5 ⊢ ∃y. (y, c) ∈ S, ex_elim 5 24.
26. 1 ⊢ ∃y. (y, c) ∈ S, ex_elim 4 25.
composition_eliml. ⊢ (a, c) ∈ S ∘ R → ∃y. (y, c) ∈ S, subj_intro 26.

27. 1, 7 ⊢ (a, y) ∈ R ∧ (y, c) ∈ S, conj_intro 16 22.
28. 1, 7 ⊢ ∃y. (a, y) ∈ R ∧ (y, c) ∈ S, ex_intro 27.
29. 1, 6 ⊢ ∃y. (a, y) ∈ R ∧ (y, c) ∈ S, ex_elim 6 28.
30. 1, 5 ⊢ ∃y. (a, y) ∈ R ∧ (y, c) ∈ S, ex_elim 5 29.
31. 1 ⊢ ∃y. (a, y) ∈ R ∧ (y, c) ∈ S, ex_elim 4 30.
composition_elim. ⊢ (a, c) ∈ S ∘ R → ∃y. (a, y) ∈ R ∧ (y, c) ∈ S,
  subj_intro 31.

01. 1 ⊢ (a, b) ∈ R, hypo.
02. 2 ⊢ (b, c) ∈ S, hypo.
03. ⊢ (a, c) = (a, c), eq_refl.
04. 1 ⊢ (a, c) = (a, c) ∧ (a, b) ∈ R, conj_intro 3 1.
05. 1, 2 ⊢ (a, c) = (a, c) ∧ (a, b) ∈ R ∧ (b, c) ∈ S, conj_intro 4 2.
06. 1, 2 ⊢ ∃z. (a, c) = (a, z) ∧ (a, b) ∈ R ∧ (b, z) ∈ S, ex_intro 5.
07. 1, 2 ⊢ ∃y. ∃z. (a, c) = (a, z) ∧ (a, y) ∈ R ∧ (y, z) ∈ S, ex_intro 6.
08. 1, 2 ⊢ ∃x. ∃y. ∃z. (a, c) = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S, ex_intro 7.
09. 1 ⊢ set (a, b), set_intro 1.
10. 2 ⊢ set (b, c), set_intro 2.
11. 1 ⊢ set a, setl_from_pair 9.
12. 2 ⊢ set c, setr_from_pair 10.
13. 1, 2 ⊢ set (a, c), set_pair 11 12.
14. 1, 2 ⊢ (a, c) ∈ {t | ∃x. ∃y. ∃z. t = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S},
  comp_intro 13 8, P t ↔ (∃x. ∃y. ∃z. t = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S).
15. ⊢ S ∘ R = {t | ∃x. ∃y. ∃z. t = (x, z) ∧ (x, y) ∈ R ∧ (y, z) ∈ S},
  composition_eq.
16. 1, 2 ⊢ (a, c) ∈ S ∘ R, eq_subst_rev 15 14, P u ↔ (a, c) ∈ u.
composition_intro. ⊢ (a, b) ∈ R → (b, c) ∈ S → (a, c) ∈ S ∘ R,
  subj_intro_ii 16.

01. 1 ⊢ x ∈ dom (S ∘ R), hypo.
02. 1 ⊢ ∃z. (x, z) ∈ S ∘ R, dom_elim 1.
03. 3 ⊢ (x, z) ∈ S ∘ R, hypo.
04. 3 ⊢ ∃y. (x, y) ∈ R, composition_elimr 3.
05. 5 ⊢ (x, y) ∈ R, hypo.
06. 5 ⊢ x ∈ dom R, dom_intro 5.
07. 3 ⊢ x ∈ dom R, ex_elim 4 6.
08. 1 ⊢ x ∈ dom R, ex_elim 2 7.
09. ⊢ x ∈ dom (S ∘ R) → x ∈ dom R, subj_intro 8.
10. ⊢ ∀x. x ∈ dom (S ∘ R) → x ∈ dom R, uq_intro 9.
dom_composition. ⊢ dom (S ∘ R) ⊆ dom R, incl_intro 10.

01. 1 ⊢ z ∈ rng (S ∘ R), hypo.
02. 1 ⊢ ∃x. (x, z) ∈ S ∘ R, rng_elim 1.
03. 3 ⊢ (x, z) ∈ S ∘ R, hypo.
04. 3 ⊢ ∃y. (y, z) ∈ S, composition_eliml 3.
05. 5 ⊢ (y, z) ∈ S, hypo.
06. 5 ⊢ z ∈ rng S, rng_intro 5.
07. 3 ⊢ z ∈ rng S, ex_elim 4 6.
08. 1 ⊢ z ∈ rng S, ex_elim 2 7.
09. ⊢ z ∈ rng (S ∘ R) → z ∈ rng S, subj_intro 8.
10. ⊢ ∀z. z ∈ rng (S ∘ R) → z ∈ rng S, uq_intro 9.
rng_composition. ⊢ rng (S ∘ R) ⊆ rng S, incl_intro 10.

01. 1 ⊢ z ∈ img (S ∘ R) A, hypo.
02. 1 ⊢ ∃x. x ∈ A ∧ (x, z) ∈ S ∘ R, img_elim 1.
03. 3 ⊢ x ∈ A ∧ (x, z) ∈ S ∘ R, hypo.
04. 3 ⊢ x ∈ A, conj_eliml 3.
05. 3 ⊢ (x, z) ∈ S ∘ R, conj_elimr 3.
06. 3 ⊢ ∃y. (x, y) ∈ R ∧ (y, z) ∈ S, composition_elim 5.
07. 7 ⊢ (x, y) ∈ R ∧ (y, z) ∈ S, hypo.
08. 7 ⊢ (x, y) ∈ R, conj_eliml 7.
09. 3, 7 ⊢ y ∈ img R A, img_intro 4 8.
10. 7 ⊢ (y, z) ∈ S, conj_elimr 7.
11. 3, 7 ⊢ z ∈ img S (img R A), img_intro 9 10.
12. 3 ⊢ z ∈ img S (img R A), ex_elim 6 11.
13. 1 ⊢ z ∈ img S (img R A), ex_elim 2 12.
14. ⊢ z ∈ img (S ∘ R) A → z ∈ img S (img R A), subj_intro 13.
15. 15 ⊢ z ∈ img S (img R A), hypo.
16. 15 ⊢ ∃y. y ∈ img R A ∧ (y, z) ∈ S, img_elim 15.
17. 17 ⊢ y ∈ img R A ∧ (y, z) ∈ S, hypo.
18. 17 ⊢ y ∈ img R A, conj_eliml 17.
19. 17 ⊢ ∃x. x ∈ A ∧ (x, y) ∈ R, img_elim 18.
20. 20 ⊢ x ∈ A ∧ (x, y) ∈ R, hypo.
21. 20 ⊢ x ∈ A, conj_eliml 20.
22. 20 ⊢ (x, y) ∈ R, conj_elimr 20.
23. 17 ⊢ (y, z) ∈ S, conj_elimr 17.
24. 17, 20 ⊢ (x, z) ∈ S ∘ R, composition_intro 22 23.
25. 17, 20 ⊢ z ∈ img (S ∘ R) A, img_intro 21 24.
26. 17 ⊢ z ∈ img (S ∘ R) A, ex_elim 19 25.
27. 15 ⊢ z ∈ img (S ∘ R) A, ex_elim 16 26.
28. ⊢ z ∈ img S (img R A) → z ∈ img (S ∘ R) A, subj_intro 27.
29. ⊢ z ∈ img (S ∘ R) A ↔ z ∈ img S (img R A), bij_intro 14 28.
30. ⊢ ∀z. z ∈ img (S ∘ R) A ↔ z ∈ img S (img R A), uq_intro 29.
img_composition. ⊢ img (S ∘ R) A = img S (img R A), ext 30.

01. 1 ⊢ x ∈ inv_img (S ∘ R) B, hypo.
02. 1 ⊢ ∃z. z ∈ B ∧ (x, z) ∈ S ∘ R, inv_img_elim 1.
03. 3 ⊢ z ∈ B ∧ (x, z) ∈ S ∘ R, hypo.
04. 3 ⊢ z ∈ B, conj_eliml 3.
05. 3 ⊢ (x, z) ∈ S ∘ R, conj_elimr 3.
06. 3 ⊢ ∃y. (x, y) ∈ R ∧ (y, z) ∈ S, composition_elim 5.
07. 7 ⊢ (x, y) ∈ R ∧ (y, z) ∈ S, hypo.
08. 7 ⊢ (y, z) ∈ S, conj_elimr 7.
09. 3, 7 ⊢ y ∈ inv_img S B, inv_img_intro 4 8.
10. 7 ⊢ (x, y) ∈ R, conj_eliml 7.
11. 3, 7 ⊢ x ∈ inv_img R (inv_img S B), inv_img_intro 9 10.
12. 3 ⊢ x ∈ inv_img R (inv_img S B), ex_elim 6 11.
13. 1 ⊢ x ∈ inv_img R (inv_img S B), ex_elim 2 12.
14. ⊢ x ∈ inv_img (S ∘ R) B → x ∈ inv_img R (inv_img S B), subj_intro 13.
15. 15 ⊢ x ∈ inv_img R (inv_img S B), hypo.
16. 15 ⊢ ∃y. y ∈ inv_img S B ∧ (x, y) ∈ R, inv_img_elim 15.
17. 17 ⊢ y ∈ inv_img S B ∧ (x, y) ∈ R, hypo.
18. 17 ⊢ y ∈ inv_img S B, conj_eliml 17.
19. 17 ⊢ ∃z. z ∈ B ∧ (y, z) ∈ S, inv_img_elim 18.
20. 20 ⊢ z ∈ B ∧ (y, z) ∈ S, hypo.
21. 20 ⊢ z ∈ B, conj_eliml 20.
22. 20 ⊢ (y, z) ∈ S, conj_elimr 20.
23. 17 ⊢ (x, y) ∈ R, conj_elimr 17.
24. 17, 20 ⊢ (x, z) ∈ S ∘ R, composition_intro 23 22.
25. 17, 20 ⊢ x ∈ inv_img (S ∘ R) B, inv_img_intro 21 24.
26. 17 ⊢ x ∈ inv_img (S ∘ R) B, ex_elim 19 25.
27. 15 ⊢ x ∈ inv_img (S ∘ R) B, ex_elim 16 26.
28. ⊢ x ∈ inv_img R (inv_img S B) → x ∈ inv_img (S ∘ R) B, subj_intro 27.
29. ⊢ x ∈ inv_img (S ∘ R) B ↔ x ∈ inv_img R (inv_img S B), bij_intro 14 28.
30. ⊢ ∀x. x ∈ inv_img (S ∘ R) B ↔ x ∈ inv_img R (inv_img S B), uq_intro 29.
inv_img_composition. ⊢ inv_img (S ∘ R) B = inv_img R (inv_img S B), ext 30.


(*/section Special relations *)

01. ⊢ ∅ ⊆ X × Y, empty_set_is_least.
empty_relation. ⊢ relation ∅, relation_from_incl 1.

01. 1 ⊢ x ∈ dom ∅, hypo.
02. 1 ⊢ ∃y. (x, y) ∈ ∅, dom_elim 1.
03. 3 ⊢ (x, y) ∈ ∅, hypo.
04. 3 ⊢ x ∈ ∅, empty_efq 3.
05. 1 ⊢ x ∈ ∅, ex_elim 2 4.
06. ⊢ x ∈ dom ∅ → x ∈ ∅, subj_intro 5.
07. ⊢ ∀x. x ∈ dom ∅ → x ∈ ∅, uq_intro 6.
08. ⊢ dom ∅ ⊆ ∅, incl_intro 7.
09. ⊢ ∅ ⊆ dom ∅, empty_set_is_least.
dom_empty_set. ⊢ dom ∅ = ∅, incl_antisym 8 9.

01. 1 ⊢ y ∈ rng ∅, hypo.
02. 1 ⊢ ∃x. (x, y) ∈ ∅, rng_elim 1.
03. 3 ⊢ (x, y) ∈ ∅, hypo.
04. 3 ⊢ y ∈ ∅, empty_efq 3.
05. 1 ⊢ y ∈ ∅, ex_elim 2 4.
06. ⊢ y ∈ rng ∅ → y ∈ ∅, subj_intro 5.
07. ⊢ ∀y. y ∈ rng ∅ → y ∈ ∅, uq_intro 6.
08. ⊢ rng ∅ ⊆ ∅, incl_intro 7.
09. ⊢ ∅ ⊆ rng ∅, empty_set_is_least.
rng_empty_set. ⊢ rng ∅ = ∅, incl_antisym 8 9.

01. 1 ⊢ set a, hypo.
02. 2 ⊢ set b, hypo.
03. 1, 2 ⊢ set (a, b), set_pair 1 2.
04. 4 ⊢ t ∈ {(a, b)}, hypo.
05. 1, 2, 4 ⊢ t = (a, b), sg_elim 3 4.
06. 1, 2, 4 ⊢ ∃y. t = (a, y), ex_intro 5.
07. 1, 2, 4 ⊢ ∃x. ∃y. t = (x, y), ex_intro 6.
08. 1, 2 ⊢ t ∈ {(a, b)} →  ∃x. ∃y. t = (x, y), subj_intro 7.
09. 1, 2 ⊢ ∀t. t ∈ {(a, b)} →  ∃x. ∃y. t = (x, y), uq_intro 8.
10. 1, 2 ⊢ relation {(a, b)}, relation_fold 9.
sg_relation. ⊢ set a → set b → relation {(a, b)}, subj_intro_ii 10.

01. 1 ⊢ set a, hypo.
02. 2 ⊢ set b, hypo.
03. 3 ⊢ x ∈ dom {(a, b)}, hypo.
04. 3 ⊢ ∃y. (x, y) ∈ {(a, b)}, dom_elim 3.
05. 5 ⊢ (x, y) ∈ {(a, b)}, hypo.
06. 1, 2 ⊢ set (a, b), set_pair 1 2.
07. 1, 2, 5 ⊢ (x, y) = (a, b), sg_elim 6 5.
08. 1, 2, 5 ⊢ (a, b) = (x, y), eq_symm 7.
09. 1, 2, 5 ⊢ a = x ∧ b = y, pair_property 1 2 8.
10. 1, 2, 5 ⊢ a = x, conj_eliml 9.
11. 1, 2, 5 ⊢ x = a, eq_symm 10.
12. 1, 2, 3 ⊢ x = a, ex_elim 4 11.
13. 1, 2, 3 ⊢ x ∈ {a}, sg_intro 1 12.
14. 1, 2 ⊢ x ∈ dom {(a, b)} → x ∈ {a}, subj_intro 13.
15. 15 ⊢ x ∈ {a}, hypo.
16. 1, 15 ⊢ x = a, sg_elim 1 15.
17. ⊢ (a, b) = (a, b), eq_refl.
18. 1, 15 ⊢ (x, b) = (a, b), eq_subst_rev 16 17, P u ↔ (u, b) = (a, b).
19. 1, 2, 15 ⊢ (x, b) ∈ {(a, b)}, sg_intro 6 18.
20. 1, 2, 15 ⊢ x ∈ dom {(a, b)}, dom_intro 19.
21. 1, 2 ⊢ x ∈ {a} → x ∈ dom {(a, b)}, subj_intro 20.
22. 1, 2 ⊢ x ∈ dom {(a, b)} ↔ x ∈ {a}, bij_intro 14 21.
23. 1, 2 ⊢ ∀x. x ∈ dom {(a, b)} ↔ x ∈ {a}, uq_intro 22.
24. 1, 2 ⊢ dom {(a, b)} = {a}, ext 23.
dom_sg. ⊢ set a → set b → dom {(a, b)} = {a}, subj_intro_ii 24.

01. 1 ⊢ set a, hypo.
02. 2 ⊢ set b, hypo.
03. 3 ⊢ y ∈ rng {(a, b)}, hypo.
04. 3 ⊢ ∃x. (x, y) ∈ {(a, b)}, rng_elim 3.
05. 5 ⊢ (x, y) ∈ {(a, b)}, hypo.
06. 1, 2 ⊢ set (a, b), set_pair 1 2.
07. 1, 2, 5 ⊢ (x, y) = (a, b), sg_elim 6 5.
08. 1, 2, 5 ⊢ (a, b) = (x, y), eq_symm 7.
09. 1, 2, 5 ⊢ a = x ∧ b = y, pair_property 1 2 8.
10. 1, 2, 5 ⊢ b = y, conj_elimr 9.
11. 1, 2, 5 ⊢ y = b, eq_symm 10.
12. 1, 2, 3 ⊢ y = b, ex_elim 4 11.
13. 1, 2, 3 ⊢ y ∈ {b}, sg_intro 2 12.
14. 1, 2 ⊢ y ∈ rng {(a, b)} → y ∈ {b}, subj_intro 13.
15. 15 ⊢ y ∈ {b}, hypo.
16. 2, 15 ⊢ y = b, sg_elim 2 15.
17. ⊢ (a, b) = (a, b), eq_refl.
18. 2, 15 ⊢ (a, y) = (a, b), eq_subst_rev 16 17, P u ↔ (a, u) = (a, b).
19. 1, 2, 15 ⊢ (a, y) ∈ {(a, b)}, sg_intro 6 18.
20. 1, 2, 15 ⊢ y ∈ rng {(a, b)}, rng_intro 19.
21. 1, 2 ⊢ y ∈ {b} → y ∈ rng {(a, b)}, subj_intro 20.
22. 1, 2 ⊢ y ∈ rng {(a, b)} ↔ y ∈ {b}, bij_intro 14 21.
23. 1, 2 ⊢ ∀y. y ∈ rng {(a, b)} ↔ y ∈ {b}, uq_intro 22.
24. 1, 2 ⊢ rng {(a, b)} = {b}, ext 23.
rng_sg. ⊢ set a → set b → rng {(a, b)} = {b}, subj_intro_ii 24.

01. 1 ⊢ set a, hypo.
02. 2 ⊢ set b, hypo.
03. 3 ⊢ y ∈ img {(a, b)} {a}, hypo.
04. 3 ⊢ ∃x. x ∈ {a} ∧ (x, y) ∈ {(a, b)}, img_elim 3.
05. 5 ⊢ x ∈ {a} ∧ (x, y) ∈ {(a, b)}, hypo.
06. 5 ⊢ (x, y) ∈ {(a, b)}, conj_elimr 5.
07. 1, 2 ⊢ set (a, b), set_pair 1 2.
08. 1, 2, 5 ⊢ (x, y) = (a, b), sg_elim 7 6.
09. 1, 2, 5 ⊢ (a, b) = (x, y), eq_symm 8.
10. 1, 2, 5 ⊢ a = x ∧ b = y, pair_property 1 2 9.
11. 1, 2, 5 ⊢ b = y, conj_elimr 10.
12. 1, 2, 5 ⊢ y = b, eq_symm 11.
13. 1, 2, 5 ⊢ y ∈ {b}, sg_intro 2 12.
14. 1, 2, 3 ⊢ y ∈ {b}, ex_elim 4 13.
15. 1, 2 ⊢ y ∈ img {(a, b)} {a} → y ∈ {b}, subj_intro 14.
16. 16 ⊢ y ∈ {b}, hypo.
17. 2, 16 ⊢ y = b, sg_elim 2 16.
18. ⊢ (a, b) = (a, b), eq_refl.
19. 2, 16 ⊢ (a, y) = (a, b), eq_subst_rev 17 18,
  P u ↔ (a, u) = (a, b).
20. 1, 2, 16 ⊢ (a, y) ∈ {(a, b)}, sg_intro 7 19.
21. ⊢ a = a, eq_refl.
22. 1 ⊢ a ∈ {a}, sg_intro 1 21.
23. 1, 2, 16 ⊢ y ∈ img {(a, b)} {a}, img_intro 22 20.
24. 1, 2 ⊢ y ∈ {b} → y ∈ img {(a, b)} {a}, subj_intro 23.
25. 1, 2 ⊢ y ∈ img {(a, b)} {a} ↔ y ∈ {b}, bij_intro 15 24.
26. 1, 2 ⊢ ∀y. y ∈ img {(a, b)} {a} ↔ y ∈ {b}, uq_intro 25.
27. 1, 2 ⊢ img {(a, b)} {a} = {b}, ext 26.
img_sg_sg. ⊢ set a → set b → img {(a, b)} {a} = {b}, subj_intro_ii 27.

01. 1 ⊢ set a, hypo.
02. 2 ⊢ set b, hypo.
03. 3 ⊢ set x, hypo.
04. 4 ⊢ ¬a = x, hypo.
05. 5 ⊢ y ∈ img {(a, b)} {x}, hypo.
06. 5 ⊢ ∃u. u ∈ {x} ∧ (u, y) ∈ {(a, b)}, img_elim 5.
07. 7 ⊢ u ∈ {x} ∧ (u, y) ∈ {(a, b)}, hypo.
08. 7 ⊢ u ∈ {x}, conj_eliml 7.
09. 3, 7 ⊢ u = x, sg_elim 3 8.
10. 1, 2 ⊢ set (a, b), set_pair 1 2.
11. 7 ⊢ (u, y) ∈ {(a, b)}, conj_elimr 7.
12. 1, 2, 7 ⊢ (u, y) = (a, b), sg_elim 10 11.
13. 1, 2, 7 ⊢ (a, b) = (u, y), eq_symm 12.
14. 1, 2, 7 ⊢ a = u ∧ b = y, pair_property 1 2 13.
15. 1, 2, 7 ⊢ a = u, conj_eliml 14.
16. 1, 2, 3, 7 ⊢ a = x, eq_trans 15 9.
17. 1, 2, 3, 5 ⊢ a = x, ex_elim 6 16.
18. 1, 2, 3, 4, 5 ⊢ ⊥, neg_elim 4 17.
19. 1, 2, 3, 4, 5 ⊢ y ∈ ∅, efq 18.
20. 1, 2, 3, 4 ⊢ y ∈ img {(a, b)} {x} → y ∈ ∅, subj_intro 19.
21. 1, 2, 3, 4 ⊢ ∀y. y ∈ img {(a, b)} {x} → y ∈ ∅, uq_intro 20.
22. 1, 2, 3, 4 ⊢ img {(a, b)} {x} ⊆ ∅, incl_intro 21.
23. ⊢ ∅ ⊆ img {(a, b)} {x}, empty_set_is_least.
24. 1, 2, 3, 4 ⊢ img {(a, b)} {x} = ∅, incl_antisym 22 23.
img_sg_sg_neq. ⊢ set a → set b → set x → ¬a = x →
  img {(a, b)} {x} = ∅, subj_intro_iv 24.

